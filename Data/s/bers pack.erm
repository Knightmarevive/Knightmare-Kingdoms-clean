ZVSE

disabled some options by majaczek


; <# Call to Arms - allows to instantly collect creatures from dwellings into town. Option: 994.
; ƒать отр€д в гарнизон текущего города, если там есть место
; јргументы: “ип монстров,  ол-во
!?FU100500;
; ¬ызывать через FOR 0 TO 6 DO
; ¬озвращает "Result": boolean
!!SN:W^Result^/0;
!!CA-1:H0/?y50;
!!if&y50=-1:;
  !!CA-1:M2/x16/?y1/?y2;
  !!FU&y1<>-1/y1<>x1:E;
  !!CA-1:M2/x16/x1/dx2;
!!el:;
  !!HEy50:C0/x16/?y1/?y2;
  !!FU&y1<>-1/y1<>x1:E;
  !!HEy50:C0/x16/x1/dx2;
!!en:;
!!SN:W^Result^/1;
!!VRx16:S6;

; ѕолучить тип ресурса, который требуетс€ дл€ найма указанного монстра. -1 - нет.
; јргументы: “ип монстра, ?“ип ресурса
!?FU100501;
!!VRx2:S-1;         результат по умолчанию отрицательный
!!VRy1:S0;          #1  y1 - счЄтчик
!!MA:Cx1/y1/?y2;    y2 - кол-во ресурса y1
!!VRy1:+1;
!!SN&y2=0/y1<=5:G2; если очередной ресурс не требуетс€ и не все ресурсы обработаны
!!VRx2&y2>0:Sy1-1;  установим результат

; Ќажатие клавиши
!?FU77003;
!!UN:P994/?y1;
!!FU&y1<>1:E;

!!SN:L^Era.dll^/?y1 Ay1/^GetGameState^/?y2 Ey2/1/?y98; #2
!!FU&y99<>6053040:E; выйти, если не экран города
!!SN:X?y1;
!!FU|y1<49/y1>55:E; выйти, если не клавиша 1..7
!!SN:X?y1/1;        запретить реакцию по умочанию
!!VRy1:-49;         y1 - уровень монстров (0..6)
!!CA-1:O?y2 T?y5;   y2 - хоз€ин города, y5 - тип города
!!VRv2:S-1;
!!VRz3:S^%Y5^;
!!VRz4:S^Data\s\dwel.ini^;
!!UN:N6/z2/y1/3/4;  загрузить тип жилища монстров под данный город и уровень
!!VRy3:Vz2;         y3 - тип жилища
!!UN:U17/y3/?y4;    y4 - кол-во таких дилищ на карте
!!FU&y4=0:E;        выход, если нет жилищ
!!VRy6:S0;          y6 - счЄтчик цикла
!!OW:R-1/6/?y60;    y60 - золото до цикла

!!UN:U17/y3/-1/2;               #18  нашли следующее жилище: v2, v3, v4
!!DW2:O?y7 M0/?y8/?y9;          y7 - хоз€ин жилища, y8 - тип монстров, y9 - кол-во

!!if&y2=y7/y9>0:;               если хоз€ин совпадает с нашим и есть монстры
  !!if&y1=0:;                   дл€ существ 1-го уровн€ найм бесплатный
    !!DO100500/0/6/1:Py8/y9;    даЄм отр€д
    !!SN:W^Result^/?y50;
    !!if&y50=0:;
      !!VRy51:Sy9*65536+y8;
      !!if&y9=1:;
        !!UN:N3/-1/y8/0;
        !!VRz-2:S^needs^;
      !!el:;
        !!UN:N3/-1/y8/1;
        !!VRz-2:S^need^;
      !!en:;
      !!IF:Q1/21/y51/1^%Z-1 %Z-2 more space in garrison^;
      !!UN:R4;
      !!FU:E;
    !!en:;
    !!DW2:M0/d/0;               очищаем жилище
  !!el:;
    !!MA:Cy8/6/?y10;            y10 - стоимость одного монстра в жилище в золоте
    !!VRy10:*122 :100;          [majaczek] counted 22% tax
    !!OW:R-1/6/?y11;            y11 - золото игрока
    !!VRy12:Sy11:y10;           y12 - максимальное кол-во монстров, которое игрок может купить
    !!FU100501:Py8/?v5;         v5 - тип ресурса дл€ монстров
    !!if&v5<>-1:;               если нужен ресурс
      !!OW:R-1/v5/?y13;         y13 - кол-во ресурса у игрока
      !!MA:Cy8/v5/?y14;         y14 - цена одного монстра в ресурсах
      !!VRy15:Sy13:y14;         y15 - максимальное кол-во монстров дл€ найма с точки зрени€ ресурсов
      !!VRy12&y15<y12:Sy15;     вывели кол-во монстров, которое игрок может купить, исход€ из всех ресурсов
    !!en:;
    !!VRy12&y12>y9:Sy9;         скорректировали y12, если игрок может купить больше, чем есть
    !!if&y12>0:;                если игрок хоть кого-то может купить
      !!DO100500/0/6/1:Py8/y12; даЄм отр€д
      !!SN:W^Result^/?y50;
      !!if&y50=0:;
        !!VRy51:Sy9*65536+y8;
        !!if&y9=1:;
          !!UN:N3/-1/y8/0;
          !!VRz-2:S^needs^;
        !!el:;
          !!UN:N3/-1/y8/1;
          !!VRz-2:S^need^;
        !!en:;
        !!IF:Q1/21/y51/1^%Z-1 %Z-2 more space in garrison^;
        !!UN:R4;
        !!FU:E;
      !!en:;
      !!VRy12:*-1;
      !!VRy16:Sy12*y10;         y16 - цена за выкуп в золоте
      !!OW:R-1/6/dy16;          вычли золото
      !!if&v5<>-1:;             если нужно платить ресурсом
        !!VRy17:Sy12*y14;       y17 - цена за выкуп в ресурсе
        !!OW:R-1/v5/dy17;       вычли ресурсы
      !!en:;
      !!DW2:M0/d/dy12;          уменьшаем количество монстров в жилище
      !!VRy12:*-1;
    !!en:;
  !!en:;
!!en:;
!!VRy6:+1;
!!SN&y6<y4:G18;

!!OW:R-1/6/?y61; y61 - золото после наЄма
!!if&y61<>y60:;
  !!VRz-1:S^gold.wav^;
  !!SN:Pz-1;
!!en:;
!!UN:R4;
; #>
