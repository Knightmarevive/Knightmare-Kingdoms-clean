ZVSE
ERMS_ScriptDate=21.1(January).2013
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : DracoLich
** Name          : Skeletons with artifacts
** Name rus.     : Скелеты с артефактами
** Changes rus.  : [Algor] вынос опции в отдельный файл для мода ERA, построчные комментарии
**                 [Algor] Замена TM25 на TM19(стандартный)
**                 [Algor] скелет теперь всегда охраняет артефакт ценнее treasure
**                 [Algor] Параметры и способности скелета-охранника привязаны к ценности артефакта.
**                 [Algor] Для разных классов артефактов разные запросы на битву.
**                 [Algor] вынос текстов в ert-файл
**                 [Algor] перенумерация функций/переменных и пр.
** Options       : 882
** Dialogs       : -
** Variables     : -
** Tmp variables : v1-v6, z1
** Timers        : TM19 (standard)
** Functions     : FU7936
** PO-values     : -

!#TM19:S1/1/1/255;            [первый день для всех игроков]

!?TM19;                       [в первый день первого игрока]
!!UN:P882/?y1;                [проверяем включена ли опция в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!UN:P882/2;                  [предотвращаем повторное выполнение]
!!UN:U22/-1/?y1;              [y1 - количество трупов]
!!VRv1:S-1;                   [инициализация v1 для быстрого поиска]
!!DO97936/1/y1/1:P;            [для каждого трупа ...]

!?FU97936;                     [раздаем трупам артефакты]
!!UN:U22/-1/-1/1;             [получаем координаты трупа в v1-v3]
!!VRy1:S0 R100;               [y1 - случайное число (0.100)]
!!VRy2&y1>0:S2;               [В 40% случаев treasure-артефакт]
!!VRy2&y1>40:S4;              [В 30% случаев minor-артефакт]
!!VRy2&y1>70:S8;              [В 20% случаев major-артефакт]
!!VRy2&y1>90:S16;             [В 10% случаев relict-артефакт]
!!UN:J6/y2/?y1;               [y1 - случайный артефакт нужного класса]
!!PO1&y3=3:Ny2;               [устанавливаем ценность артефакта (2/4/8/16) в пользовательское число клетки с трупом]
!!SK1:S1 Ay1;                 [даем трупу артефакт]

!?OB22;                       [посещение трупа]
!!UN:P882/?y1;                [проверяем включена ли опция в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!OB998&-1000:R;              [при посещении ИИ разрешаем использование объекта]
!!PO998&-1000:N0;             [... и обнуляем пользовательское число клетки с трупом]
!!FU&-1000:E;                 [ИИ не сражается со скелетом]
!!PO998:N?y1;                 [y1 - ценность артефакта (2/4/8/16)]
!!OB998&y1<4:R;               [для отсутствующих или treasure-артефактов разрешаем стандартное использование объекта]
!!PO998&y1<4:N0;              [... сбрасываем пользовательское число]
!!FU&y1<4:E;                  [... и выходим]
!!HE-1:N?y2;                  [y2 - номер героя]
!!VRz1&y1=4:Sz179096;         [сообщение для minor-артефакта]
!!VRz1&y1=8:Sz179097;         [сообщение для major-артефакта]
!!VRz1&y1=16:Sz179098;        [сообщение для relict-артефакта]
!!IF:Q1/21/56/2^%Z1^;         [выводим запрос на битву]
!!OB998&-1:S;                 [запрещаем использовать объект, если игрок отказался сражаться]
!!FU&-1:E;                    [... и выходим]
!!UN:P882/y1;                 [выставляем класс артефакта]
!!HEv5058:Tv998/v999/v1000/56/1; [инициируем битву с 1 скелетом]
!!HEy2:O?y1;                  [y1 - хозяин героя]
!!FU&y1=-1:E;                 [выход, если герой проиграл]
!!PO998:N0;                   [сбрасываем пользовательское число]
!!OB998:R;                    [разрешаем стандартное использование объекта]

!?BF;
!!UN:P882/?y1;                [проверяем включена ли опция или класс артефакта в y1]
!!FU&y1<4:E;                  [выход, если опция не включена или это не бой со скелетом-охранником]
!!UN:P882/2;                  [сбрасываем класс артефакта для боя]
!!VRv1&y1=4:C10/10/5/300/50/100;[определяем параметры скелета-защитника]
!!VRv1&y1=8:C20/20/10/600/100/200;[...для класса артефакта в v1-v6]
!!VRv1&y1=16:C30/30/15/900/150/300;[...att def sp h dl dm]
!!BM21:F?y2;                  [y2 - флаг скелета]
!!VRy2&y1>2:|65536;           [безответный удар для minor-артефактов]
!!VRy2&y1>4:|65536;           [+ двойной удар для major-артефактов]
!!VRy2&y1>8:|65536;           [+ круговая атака для relict-артефактов]
!!BM21:Av1 Dv2 Sv3 Hv4 U1/v5 U2/v6 Fy2;[выставляем способности скелету-охраннику]

** end
