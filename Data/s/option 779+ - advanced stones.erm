ZVSE
ERMS_ScriptDate=14.10(October).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Algor
** Name          : Advanced stones
** Name rus.     : Камни самопознания
** Options       : 779
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : -
** PO-values     : -

!?OB100;                      [При посещении героем камня знаний]
!!UN:P779/?y1;                [проверяем включена ли опция 779 в y1]
!!SN:W^more_SS_levels.era^/?y88;
!!FU&y1=0:E;                  [выход если опция не включена]
!!ST998:N?y1;                 [y1 - номер камня 0..31]
!!HE-1:V0/y1/?y2;             [y2=1 если камень уже посещался героем]
!!FU&y2=1:E;                  [выход если камень уже посещался героем]
!!HE-1:V0/y1/1 E?y1/?y2 S21/?y3; [устанавливаем флаг посещения камня героем, y1 - опыт героя, y2 - уровень героя, y3 - уровень обучения (0..3)]
!!VRy2:*100 +900;             [y2 - опыт который дает камень: 900 + 100 за каждый уровень героя]
!!VRy3&y88=0:*y2 :20;               [y3 - +5% опыта за уровень Обучения]
!!FU(SSL_get)&y88<>0:P21/y3/?y7/?y8/?y9/?y10;
!_!VRy8&y88<>0:+10000;
!!VRy3&y88<>0:Sy2 *y8 :10000;
!!VRy2:+y3;                   [y2 - опыт с учетом обучения]
!!VRy1:+y2;                   [расчитываем новый опыт героя]
!!HE-1:O?y3;                  [y3 - хозяин героя]
!!OW:Iy3/?y4;                 [y4=0, если хозяин - человек]
!!VRz1&y4=0:Sz179033;         [получаем сообщение в z1 если игрок человек]
!!IF&y4=0:Q1/17/y2/1^%Z1^;    [выводим сообщение если игрок человек]
!!HE-1:Ey1;                   [обновляем опыт героя]
!!OB998:S;                    [запрет посещения объекта, для скрытия стандартного сообщения]

!$OB100;                      [После посещения героем камня знаний]
!!OB998:R;                    [разрешение посещения объекта]

** end
