ZV_SE



!?FU(initDL636);
!!SN:W^LastHero_Market_of_Time^/?y1;
!!SN:W^more_SS_levels.era^/?y88; 

!!VRy83:S3; !!VRy83&y88<>0:S15;

!!HEy1:Sx16/?y2;                       [получить параметр вторичного навыка
!!VRy3:Sx16 +20;                       [номер навыка +20 (для элемента диалога)
!!VRy13:Sx16 +50;                      [номер навыка +50 (для элемента обводки диалога)
!!VRy23:Sx16 +120;                     [для HPbar]
!!VRy90:Sy2;                           [если 3, то хранит, что навык на эксперте]
!!if&y88=0:;
  !!VRy2&y2=3:-1;                        [для постройки отображения]
  !!VRy5:Sx16 *3 +3 +y2;                 [определение кадра дефа]
!!el:;
  !_!VRy2&y2=3:-1;                          *add Majaczek
  !!VRy5:Sx16 *16 +16 +y2;                  *add Majaczek  
!!en:;

!!DL636:Ay3/4/y5/1;                    [установка кадра дефа скила]
!!DL636:Ay13/4/0/1;                    [установка кадра дефа обводки / 0 кадр - без обводки]

!!if&y88=0:;
  !!VRz80&y90=3:S^YSecSkillN.def^;       [название дефа для пустого места]
  !!DL636&y90=3:Ay3/9/z80/1 Ay3/4/x16/1; [установка названия дефа и кадра]
!!el:;
  !!VRz80&y90=0:S^YSecSkillN.def^;       [название дефа для пустого места]
  !!DL636&y90=0:Ay3/9/z80/1 Ay3/4/x16/1; [установка названия дефа и кадра]
!!en:;


!!if&y88=0:;
  !!DL636&y90=3:Ay13/4/2/1;                [установка кадра дефа обводки / 2 кадр - если скил на эксперте]
  !!DL636:Ay23/4/y90/1;
  !!DL636&y90=3:Ay23/4/4/1;
!!el:;
  !!DL636&y90=15:Ay13/4/2/1;                [установка кадра дефа обводки / 2 кадр - если скил на эксперте]
  !!DL636:Ay23/4/y90/1;
  !!DL636&y90=15:Ay23/4/16/1;
!!en:;

!!SN:W^Market_of_Time_Count^/?y96; [majaczek]

!!SN:W^Market_of_Time_Locked_%X16^/0;
!_!SN&y96>=24/y2=0:W^Market_of_Time_Locked_%X16^/4;
!!SN&y2>=y83:W^Market_of_Time_Locked_%X16^/2;
!!SN&y2=0:W^Market_of_Time_Locked_%X16^/4;

; подсказка
!!UN:N4/z9/x16;                        [получить название втор.навыка]
!!if&y90<y83:;                         [если навык не на эксперте]
   !!FU(MoP_55456):P2/356;                   [получение описания - выучить]
   !!VRz101:M2/z100/0;                 [получить только первое слово]
   !!VRz101:M4/?y10;                   [получить длину текста]
   !!VRvy10:-1;                        [отнять еденицу текста]
   !!VRzy3:M1/z101/1/y10;              [установить описание]
   !!FU(MoP_55456):P1/y2;                    [получение описания уровня втор.навыка]
   !!VRz91:S^ ^;                       [ставим пробел]
   !!VRzy3:+z91 +z100 +z91 +z9;        [создаем текст подсказки]
!!el:;                              [если навык на эксперте]
   !!FU(MoP_55456):P1/y2;                    [получение описания уровня втор.навыка]
   !!VRz91:S^ ^;                       [ставим пробел]
   !!VRzy3:Sz100 +z91 +z9;             [создаем текст подсказки]
   !!FU(MoP_55456):P2/355;                   [получение описания - уже изучено]
   !!VRzy3:+z91 +z100;                 [закончить текст подсказки]
!!en:;
!!DL636:Hy13/zy3;                      [установка описания]


!?FU(OnTownMouseClick);
!!CM:I?y1 F?y2;
!!FU|y2<>512/y1<>14:E;

!!CA-1:H1/?y3;
!!FU&y3<0:E;

!!SN:W^LastHero_Market_of_Time^/y3;

!!DL636:N^MarketOfTime_15.txt^;

!_!DL636:E30720/1;                     [кнопка ок выключена]
!_!DL636:E30721/1;                     [кнопка ок выключена]

!!DO(initDL636)/0/29/1:P;                   [проверка всех втор.навыков и постоение списка]
!!SN:W^Market_of_Time^/1;          [обнуление]
!!SN:W^Market_of_Time2^/?y50;      [проверка кол-ва втор.навыков, которые на эксперте]


!!SN:W^LastHero_Market_of_Time_Skill^/-1;
!!SN:W^Market_of_Time_Locked_All^/0; !!VRy94:S0; [majaczek]
!!re i/0/29:;
  !!VRy99:Si;
  !!SN:W^Market_of_Time_Locked_%Y99^/?y98;
  !!VRy94&y98<>0:+1;
!!en:;
!!if&y94>=30:;
  !!SN:W^Market_of_Time_Locked_All^/1;
  !!DL636:E30720/1;                   [кнопка ок выключена]
  !!DL636:E30721/1;                   [кнопка ок выключена]
!!en;


!!DL636:S?v1;                         [вызов диалога]
!!SN:W^Market_of_Time_On^/0;

!?DL&v998=636/v999>9/v999<83/v1000=13;[ЛКМ. Работа со втор.навыками]
!_!OW:A-1/?y1;                         [получить номер текущего героя]
!_!HE-1&y1=-1:N?y1;                    [если OW:A не получает номер героя, то получаем через HE-1:N]
!_!VRy1:Sx1; x1 is number of hero
!!SN:W^LastHero_Market_of_Time^/?y1;
!!SN:W^more_SS_levels.era^/?y88;                                *add Majaczek
!!VRy83:S3; !!VRy83&y88<>0:S15;

!!if&v999>49/v999<80:;                [работа со вторичными навыками]
  !!DO(refreshDL636)/120/149/1:Py1;  initialize all frames to known skill  *add Valery
  !!VRy2:Sv999+70;  get iteem ID                                  *add Valery
  !!VRy3:Sy2-120;    get skill clicked                            *add Valery

  !!SN:W^Market_of_Time_Locked_%Y3^/?y98; [majaczek]
  !!FU&y98<>0:E; !_!IF:M^%Y3 %Y98^;

  !!HEy1:Sy3/?y4; get expertise                                   *add Valery
  !_!VRy4:+1; increment by one visually, if not expert            *add Valery

  !!DL636&y4<4/y88=0:Ay2/4/y4/1;   set                               *add Valery
  !!DL636&y4<16/y88<>0:Ay2/4/y4/1; set                               *add majaczek

  !!VRy3:Sv999 -50;                   [переход к номеру втор.навыка]
  !!HEy1:Sy3/?y2;                     [получить уровень вторичного навыка]
  !!if&y2<>y83;                 [если навык не эсперт, то]
    !!DL636:E30720/1;                 [включить кнопку ок]
    !!DL636:E30721/1;                 [включить кнопку ок]
    !!SN:W^Market_of_Time_Screen^/?y99 W^Market_of_Time_Screen^/v999;

    !!DL636:Ay99/4/0/1 Av999/4/1/1;   [включить желтую обводку]
    !!SN:W^LastHero_Market_of_Time_Skill^/y3;      [запомнить выбранный втор.навык]
  !!el:;                      [если эксперт, то]
    !!DL636:E30720/0;                 [откл. кнопку ок]
    !!DL636:E30721/0;                 [откл. кнопку ок]
    !!SN:W^Market_of_Time_Screen^/?y99;   [запомнить номер элемента]
    !!DL636:Ay99/4/0/1;               [откл. желтую обводку]
  !!en:;
!!en:;

!?DL&v998=636/v999=30720/v1000=13;[закрываем диалог если Item number 30720]

!!SN:W^more_SS_levels.era^/?y88;                                *add Majaczek
!!VRy83:S3; !!VRy83&y88<>0:S15;
!!VRy84:Sy83;

!!SN:W^LastHero_Market_of_Time_Skill^/?y2;          [узнать выбранный втор.навык
!_!VRy2:S999;
!!if&y2>=0/y2=<29:;                     [если клик на втор.навыке]
  !_!OW:A-1/?y1;                        [получить номер текущего героя]
  !_!HE-1&y1=-1:N?y1;                   [если OW:A не получает номер героя, то получаем через HE-1:N]
  !!SN:W^LastHero_Market_of_Time^/?y1;
  !_!VRy1:Sx1; x1 is number of hero

  !!HEy1:Sy2/?y3;                      [узнаем уровень навыка]
  !_!VRy3:+1;                           [повышаем уровень]
  !_!HEy1&y3<y84:Sy2/y3;                [установка повышенного уровня втор.навыка]

  !_!HEy1&y3>0:Sy2/0; 
  !_!IF:M^%Y1 %Y2 %Y3^;

  !!IF:V3/0;
  !!IF&y3>0:Q3^Do you really want to forget ?^;
  !!HEy1&3:Sy2/0;

  !!IF:V4/0;
  !_!SN:W^LastHero_Market_of_Time_Skill^/-1;
  !!DO(refreshDL636)/120/149/1:Py1;

  !!IF|-3/y3<=0:Q4^Do you want to leave ?^;
  !!if&4:;
    !!DL:C1;                               [закрыть диалог]
    !!UN:R1;                               [обновить экран, чтобы видеть увеличенные перв.навыки]
  !!en;
!!el:;
  !!DL:C1;                               [закрыть диалог]
  !!UN:R1;                               [обновить экран, чтобы видеть увеличенные перв.навыки]
!!en:;


!?DL&v998=636/v999>9/v999<83/v1000=14;[диалог по правой кнопке мыши]
!_!OW:A-1/?y1;                         [получить номер текущего героя]
!_!HE-1&y1=-1:N?y1;                    [если OW:A не получает номер героя, то получаем через HE-1:N]
!_!VRy1:Sx1; x1 is number of hero

!!SN:W^more_SS_levels.era^/?y88;                                *add Majaczek
!!VRy83:S3; !!VRy83&y88<>0:S15;

!!SN:W^LastHero_Market_of_Time^/?y1;
!!if&v999>49/v999<80:;                [показ описания при ПКМ на втор.навыке]
  !!VRy2:Sv999 -50;                   [переход к номеру втор.навыка]

  !!SN:W^Market_of_Time_Locked_%Y2^/?y98; [majaczek]
  
  !!HEy1:Sy2/?y3;                     [получить уровень вторичного навыка]
  !!VRy4:Sy3;

  !!FU(MoP_55456):P2/356;              [получение описания - выучить]
  !!VRz101:M2/z100/0;            [получить только первое слово]
  !!VRz101:M4/?y10;              [получить длину текста]
  !!VRvy10:-1;                   [отнять еденицу текста]
  !!VRz1:M1/z101/1/y10;          [установить описание]
  !_!FU(MoP_55456):P1/y3;               [получение описания уровня втор.навыка]
  !!VRz91:S^ ^;                  [ставим пробел]
  !_!VRz1:+z91 +z100 +z91 +z9;    [создаем текст подсказки]
  !!VRz1: +z91 +z9;    

  !!VRy4:Sy2*3 +2 +y3;
  !!VRy4&y88<>0:Sy2 *16 +16 +y3 +1024;
  !!IF:Q1/20/y4/1^{%Z1}^;

!!en;


!?FU(refreshDL636);                                             *add Valery
;x1 hero ID                                                     *add Valery
!!SN:W^more_SS_levels.era^/?y88;                                *add Majaczek
!!VRy1:Sx16-120; get skill ID                                   *add Valery
!!HEx1:Sy1/?y2; Get skill expertise                             *add Valery

!!if&y88=0:;
  !!DL636&y2<>3:Ax16/4/y2/1;                                      *add Valery
  !!DL636&y2=3:Ax16/4/4/1;                                        *add igrik
!!el:;
  !!DL636&y2<>15:Ax16/4/y2/1;                                      *add Valery
  !!DL636&y2=15:Ax16/4/16/1;                                        *add igrik
!!en:;

!?FU(MoP_55456);
; x1 - тип текстовика
; x2 - номер строки
!!VRy100&x1=0:S6974556;  [адрес названия первичного навыка]                     PriSkill.txt
!!VRy100&x1=1:S6979432;  [адрес названия вторичного навыка]                     SkillLev.txt
!!VRy100&x1=2:S6970820;  [адрес описания что поднялся на уровень навыка]        GENRLTXT.txt

!!SN:W^more_SS_levels.era^/?y88;                                *add Majaczek
!!if&y88<>0:;

  !!if&x1=1:;
    !!VRy92:Sx2;
    !!VRz101:S^Level %Y92^;

    !!VRz100:S^^;
    !!VRz100:+z101;
    !!FU:E;
  !!en:;

!!en:;

; © Master Of Puppets
!!UN:Cy100/4/?y1;
!!VRy1:+32;
!!UN:Cy1/4/?y2;
!!VRx2:*4+y2;
!!UN:Cx2/4/?y1;
!!SN:X?y4 Xy1 X?z101 Xy4;
!!VRz100:S^^;
!!VRz100:+z101;