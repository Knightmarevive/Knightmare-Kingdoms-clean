ZVSE
ERMS_ScriptDate=3.10(October).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Algor
** Name          : Peons
** Name rus.     : Батраки
** Options       : 785
** Dialogs       : -
** Variables     : -
** Tmp variables : z1-z8
** Timers        :
** Functions     :
** PO-values     : -

---------------------------------------------------------------------------------
** fixes by majaczek
!?FU6150;  !!UN:P785/?y1;
!!SN&y1=0:L^Era.dll^/?y11 Ay11/^RedirectFile^/?y12 Ey12/1/^peons.def^/^peonsoff.def^; [если опция не включена, подменяем def батраков на прозрачный]
!?FU6151;  !!UN:P785/?y1;
!!SN&y1=0:L^Era.dll^/?y11 Ay11/^RedirectFile^/?y12 Ey12/1/^peons.def^/^peonsoff.def^; [если опция не включена, подменяем def батраков на прозрачный]
**
---------------------------------------------------------------------------------

!?FU97700;                     [перед началом игры и при загрузке]
!!UN:P785/?y1;                [y1 - состояние опции 785]
!!UN&y1=0:C6045457/4/163;     координата X           (ориг =163)
!!UN&y1=0:C6045450/1/64;      ширина строки по X      (ориг =64)
!!SN&y1=0:L^Era.dll^/?y11 Ay11/^RedirectFile^/?y12 Ey12/1/^peons.def^/^peonsoff.def^; [если опция не включена, подменяем def батраков на прозрачный]
!!UN&y1=1:C6045457/4/167;     координата X           (ориг =163)
!!UN&y1=1:C6045450/1/40;      ширина строки по X      (ориг =64)

!?CM1;                        [Клик на кнопке Батраков: инфо о доп. доходе и добавление батраков]
!!UN:P785/?y1; !!FU&y1=0:E;   [выход если опция 785 не включена]
!!CM:I?y1 F?y2 S?y3;          [y1-y3 - параметры клика]
!!SN:L^Era.dll^/?y5 Ay5/^GetButtonID^/?y6;  y2 - era.dll, y3 - function GetButtonID
!!SN:Ey6/0/^peons^; !!VRy4:Sv1; [y4 - ID кнопки "peons"]
!!FU&y1<>y4:E;                [выход, если клик не по кнопке батраков]
!!CM:R0;                      [отмена действия по умолчанию]
!!FU|y2<>0/y3<>13:E;          [выход если не отпущена ЛКМ, y1 - место клика]
!!CA-1:O?y2; !!OW:C?y3; !!FU&y2<>y3:E; [выход, если город не принадлежит игроку (город союзника)]
!!CA-1:H0/?y6;                [y6 - номер героя в гарнизоне]
!!HEy6&y6>-1:C0/0/?y10/?y20 C0/1/?y11/?y21 C0/2/?y12/?y22 C0/3/?y13/?y23 C0/4/?y14/?y24 C0/5/?y15/?y25 C0/6/?y16/?y26; [Получаем армию героя]
!!CA-1&y6>-1:M2/0/y10/y20 M2/1/y11/y21 M2/2/y12/y22 M2/3/y13/y23 M2/4/y14/y24 M2/5/y15/y25 M2/6/y16/y26; [копируем армию героя в гарнизон]
!!CA-1:P?y1/?y2/?y3;          [y1/y2/y3 - координаты города]
!!SN:W^peons_%Y1_%Y2_%Y3^/?y4;[y4 - ежедневный доход от батраков в городе]
!!CA-1:M2/0/?y10/?y20 M2/1/?y11/?y21 M2/2/?y12/?y22 M2/3/?y13/?y23 M2/4/?y14/?y24 M2/5/?y15/?y25 M2/6/?y16/?y26; [y1х/y2х - тип и количество монстров в слоте х гарнизона]
!!MA:F139/?y5; !!VRy7:Sy5 *5; !!MA:F139/y7; [y5 - оригинальное FV крестьян, увеличиваем в 5 раз]
!!MA&y20>0:Fy10/?y30;         [y30 - FV монстра в слоте 0 гарнизона]
!!MA&y21>0:Fy11/?y31;         [y31 - FV монстра в слоте 1 гарнизона]
!!MA&y22>0:Fy12/?y32;         [y32 - FV монстра в слоте 2 гарнизона]
!!MA&y23>0:Fy13/?y33;         [y33 - FV монстра в слоте 3 гарнизона]
!!MA&y24>0:Fy14/?y34;         [y34 - FV монстра в слоте 4гарнизона]
!!MA&y25>0:Fy15/?y35;         [y35 - FV монстра в слоте 5 гарнизона]
!!MA&y26>0:Fy16/?y36;         [y36 - FV монстра в слоте 6 гарнизона]
!!MA:F139/y5;                 [возврат оригинального FV крестьян]
!!VRy40&y20>0:Sy30 *y20 :50;  [y40 - доход от монстров в слоте 0 гарнизона FV*n:50 ]
!!VRy41&y21>0:Sy31 *y21 :50;  [y41 - доход от монстров в слоте 1 гарнизона FV*n:50 ]
!!VRy42&y22>0:Sy32 *y22 :50;  [y42 - доход от монстров в слоте 2 гарнизона FV*n:50 ]
!!VRy43&y23>0:Sy33 *y23 :50;  [y43 - доход от монстров в слоте 3 гарнизона FV*n:50 ]
!!VRy44&y24>0:Sy34 *y24 :50;  [y44 - доход от монстров в слоте 4 гарнизона FV*n:50 ]
!!VRy45&y25>0:Sy35 *y25 :50;  [y45 - доход от монстров в слоте 5 гарнизона FV*n:50 ]
!!VRy46&y26>0:Sy36 *y26 :50;  [y46 - доход от монстров в слоте 6 гарнизона FV*n:50 ]
!!UN&y20>0:N3/1/y10/1;        [z1 - наименование монстра в слоте 0 гарнизона]
!!UN&y21>0:N3/2/y11/1;        [z2 - наименование монстра в слоте 1 гарнизона]
!!UN&y22>0:N3/3/y12/1;        [z3 - наименование монстра в слоте 2 гарнизона]
!!UN&y23>0:N3/4/y13/1;        [z4 - наименование монстра в слоте 3 гарнизона]
!!UN&y24>0:N3/5/y14/1;        [z5 - наименование монстра в слоте 4 гарнизона]
!!UN&y25>0:N3/6/y15/1;        [z6 - наименование монстра в слоте 5 гарнизона]
!!UN&y26>0:N3/7/y16/1;        [z7 - наименование монстра в слоте 6 гарнизона]
!!VRv1:S0;                    [обнуляем v1 - результат выбора отрядов в диалоге]
!!VRz1&y20>0:+z179103;        [z1 - название 1го пункта диалога]
!!VRz2&y21>0:+z179104;        [z2 - название 1го пункта диалога]
!!VRz3&y22>0:+z179105;        [z3 - название 1го пункта диалога]
!!VRz4&y23>0:+z179106;        [z4 - название 1го пункта диалога]
!!VRz5&y24>0:+z179107;        [z5 - название 1го пункта диалога]
!!VRz6&y25>0:+z179108;        [z6 - название 1го пункта диалога]
!!VRz7&y26>0:+z179109;        [z7 - название 1го пункта диалога]
!!VRz8:Sz179023;              [получаем полный заголовок сообщения]
!!VRz8&y20=0/y21=0/y22=0/y23=0/y24=0/y25=0/y26=0:Sz179024; [получаем короткий заголовок сообщения, если в гарнизоне пусто]
!!VRy50:S0;                   [обнуление переменной]
!!VRy51:S0;                   [обнуление переменной]
!!VRy52:S0;                   [обнуление переменной]
!!VRy53:S0;                   [обнуление переменной]
!!VRy54:S0;                   [обнуление переменной]
!!VRy55:S0;                   [обнуление переменной]
!!VRy56:S0;                   [обнуление переменной]
!!VRy50&y20>0:S1;             [отображение опции 1 в диалоге, если есть отряд в слоте 0]
!!VRy51&y21>0:S2;             [отображение опции 2 в диалоге, если есть отряд в слоте 1]
!!VRy52&y22>0:S3;             [отображение опции 3 в диалоге, если есть отряд в слоте 2]
!!VRy53&y23>0:S4;             [отображение опции 4 в диалоге, если есть отряд в слоте 3]
!!VRy54&y24>0:S5;             [отображение опции 5 в диалоге, если есть отряд в слоте 4]
!!VRy55&y25>0:S6;             [отображение опции 6 в диалоге, если есть отряд в слоте 5]
!!VRy56&y26>0:S7;             [отображение опции 7 в диалоге, если есть отряд в слоте 6]
!!IF:G0/1/0/8/y50/y51/y52/y53/y54/y55/y56/0/0/0/0/0; [v1 - результат выбора. Отображение диалога]
!!VRy5:Sv1 &1;                [y5>0 если отряд в слоте 0 выбран]
!!VRy4&y5>0:+y40;             [увеличиваем доход от батраков на доход от монстров в слоте 0, если слот выбран]
!!CA-1&y5>0:M2/0/-1/0;        [убираем отряд из слота 0]
!!VRy5:Sv1 &2;                [y5>0 если отряд в слоте 1 выбран]
!!VRy4&y5>0:+y41;             [увеличиваем доход от батраков на доход от монстров в слоте 1, если слот выбран]
!!CA-1&y5>0:M2/1/-1/0;        [убираем отряд из слота 1]
!!VRy5:Sv1 &4;                [y5>0 если отряд в слоте 2 выбран]
!!VRy4&y5>0:+y42;             [увеличиваем доход от батраков на доход от монстров в слоте 2, если слот выбран]
!!CA-1&y5>0:M2/2/-1/0;        [убираем отряд из слота 2]
!!VRy5:Sv1 &8;                [y5>0 если отряд в слоте 3 выбран]
!!VRy4&y5>0:+y43;             [увеличиваем доход от батраков на доход от монстров в слоте 3, если слот выбран]
!!CA-1&y5>0:M2/3/-1/0;        [убираем отряд из слота 3]
!!VRy5:Sv1 &16;               [y5>0 если отряд в слоте 4 выбран]
!!VRy4&y5>0:+y44;             [увеличиваем доход от батраков на доход от монстров в слоте 4, если слот выбран]
!!CA-1&y5>0:M2/4/-1/0;        [убираем отряд из слота 4]
!!VRy5:Sv1 &32;               [y5>0 если отряд в слоте 5 выбран]
!!VRy4&y5>0:+y45;             [увеличиваем доход от батраков на доход от монстров в слоте 5, если слот выбран]
!!CA-1&y5>0:M2/5/-1/0;        [убираем отряд из слота 5]
!!VRy5:Sv1 &64;               [y5>0 если отряд в слоте 6 выбран]
!!VRy4&y5>0:+y46;             [увеличиваем доход от батраков на доход от монстров в слоте 6, если слот выбран]
!!CA-1&y5>0:M2/6/-1/0;        [убираем отряд из слота 6]
!!SN:W^peons_%Y1_%Y2_%Y3^/y4; [устанавливаем новый ежедневный доход от батраков в городе]
!!CA-1&y6>-1:M2/0/?y10/?y20 M2/1/?y11/?y21 M2/2/?y12/?y22 M2/3/?y13/?y23 M2/4/?y14/?y24 M2/5/?y15/?y25 M2/6/?y16/?y26; [получаем армию гарнизона]
!!HEy6&y6>-1:C0/0/y10/y20 C0/1/y11/y21 C0/2/y12/y22 C0/3/y13/y23 C0/4/y14/y24 C0/5/y15/y25 C0/6/y16/y26; [устанавливаем армию гарнизонному герою]
!!CA-1&y6>-1:M2/0/-1/0 M2/1/-1/0 M2/2/-1/0 M2/3/-1/0 M2/4/-1/0 M2/5/-1/0 M2/6/-1/0; [удаляем копию армии в гарнизоне]
!!UN:R4;                      [обновление экрана города]
!!CM:R0;                      [отключение стандартной реакции на клик]

!?FU97776;                     [расчет дохода города]             
!!SN:W^tmp.y1^/y1 W^tmp.y2^/y2 W^tmp.y3^/y3 W^tmp.y4^/y4 W^tmp.y5^/y5 W^tmp.y6^/y6 W^tmp.y7^/y7 W^tmp.y8^/y8 W^tmp.y9^/y9 W^tmp.y10^/y10 W^tmp.y11^/y11 W^tmp.y12^/y12; [сохранение значений y-переменных]
!!UN:P785/?y1;                [y1 - статус опции 785]
!!if&y1=0:;                   [если опция не включена]
  !_!SN:W^tmp.y1^/?y1;         [восстановление y1]
  !_!FU:E;                     [выход]
!!en:;
!!SN:X?y1; !!VRy2:Sy1+24;
!!UN:Cy2/4/?y3;               [структура текущего в функции города]
!!UN:Cy3/1/?y4;               [получить номер города на карте]
!!CA0/y4:P?y5/?y6/?y7;        [y5/y6/y7 - координаты города]
!!SN:W^peons_%Y5_%Y6_%Y7^/?y2;[y2 - ежедневный доход от батраков в городе]

!_!SN:W^Neutral Town %Y5 %Y6 %Y7^/?y11; !_!VRy11&y11<>0:S6000;
!!CA0/y4:T?y11; !!if&y11<9:; !!VRy11:S0; !!el:; !!VRy11:S6000; !!en:;

!!VRy3:Sy1+28; !!UN:Cy3/4/?y1;[y1 - текущий доход города]

!!CA0/y4:O?y9; !!OW:Iy9/?y10;

!!SN&y10=1:W^Knightmare Difficulty AI Base Income Multiplier^/?y8;
!!SN&y10=0:W^Knightmare Difficulty PC Base Income Multiplier^/?y8;
!!VRy8&y8=0:S100;

!!FU862:Py4/0/0/?y5/?y6;      *** majaczek
!_!VRy1:+y2; !!VRy2&y2>32000:S32000; !!VRy1:+y11 *y8 :100 +y2 +y6; [добавление дохода от батраков, макс доход города - 32к]

!!SN:W^Knightmare Skirmish Income Multiplier^/?y12;
!!if&y12=0:;
  !!VRy12:S100;  !!SN:W^Knightmare Skirmish Income Multiplier^/y12;
!!el:;
  !!VRy1:*y12 :100;
!!en;


!!UN:Cy3/4/y1;                [обновление дохода]
!!SN:W^tmp.y1^/?y1 W^tmp.y2^/?y2 W^tmp.y3^/?y3 W^tmp.y4^/?y4 W^tmp.y5^/?y5 W^tmp.y6^/?y6 W^tmp.y7^/?y7 W^tmp.y8^/?y8 W^tmp.y9^/?y9 W^tmp.y10^/?y10 W^tmp.y11^/?y11 W^tmp.y12^/?y12; [восстановление значений y-переменных]

** end
