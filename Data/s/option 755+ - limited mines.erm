ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814

** Author        : Algor (Idea from Phoenix)
** Name          : Limited mines
** Name rus.     : Истощающиеся шахты
** Options       : 755
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : FU7976-FU7977
** PO-values     : -

!#TM50:S2/999/1/255;          [каждый день для всех игроков начиная со 2го дня]

!?PI;                         [перед началом игры: установка запаса ресурсов]
!!UN:P755/?y1;                [проверяем включена ли опция 755 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!UN:X?y1/?y2;                [y1/y2 - размер/этажность карты]
!!VRy1::36 +y2;               [y1 - коэффициент от размера карты: S=1,L=2...G=7, +1 за подземелье]
!!UN:U53/-1/?y2;              [y2 - количество шахт]
!!VRv1:S-1;                   [инициализация v1 для быстрого поиска]
!!DO7976/1/y2/1:Py1;          [для каждой шахты устанавливаем запас]

!?FU7976;                     [x1 - коэффициент от размера карты]
!!UN:U53/-1/-1/1;             [получаем координаты шахты в v1-v3]
!!VRy1:Sx1 *10 Ry1;           [y1 - запас шахты S:10-20, L:20-40 ... G:70-140, +(10-20) за подземелье]
!!SN:W^opt755_%V1_%V2_%V3^/y1;[установка запаса]

!?TM50;                       [каждый день: истощение работающих шахт]
!!UN:P755/?y1;                [проверяем включена ли опция 755 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!UN:U53/-1/?y2;              [y2 - количество шахт]
!!VRv1:S-1;                   [инициализация v1 для быстрого поиска]
!!OW:C?y3;                    [y3 - текущий игрок]
!!DO7977/1/y2/1:Py3;          [для каждой работающей шахты уменьшаем запас]

!?FU7977;                     [x1 - текущий игрок]
!!UN:U53/-1/-1/1;             [получаем координаты шахты в v1-v3]
!!MNv1/v2/v3:O?y1/1;          [y1 - владелец шахты]
!!FU&y1<>x1:E;                [выход, если шахта не принадлежит текущему игроку]
!!SN:W^opt755_%V1_%V2_%V3^/?y1;[y1 - текущий запас]
!!VRy1:-1;                    [истощение]
!!SN:W^opt755_%V1_%V2_%V3^/y1;[обновление запаса]
!!FU&y1>0:E;                  [выход, если шахта не истощена]
!!MNv1/v2/v3:O-1;             [убираем владельца]
!!TRv1/v2/v3:T?y2/?i/?i/?i/?i/?i/?i/?i;[y2 - тип земли]
!!UN:Iv1/v2/v3/1/0/91/0/y2/1; [вешаем "знак" <Путо> на вход]
!!TRv1/v2/v3:E1 P0;           [закрываем вход]

!?CM0;                        [клик на карте приключений]
!!UN:P755/?y1;                [проверяем включена ли опция 755 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!CM:P?y1/?y2/?y3 F?y4;       [координаты и параметры клика]
!!OBy1/y2/y3:T?y5;            [y5 - тип объекта]
!!FU|y4<>512/y5<>53:E;        [выход, если не ПКМ или не на шахте]
!!MNy1/y2/y3:R?y6/1;          [y6 - ресурс]
!!SN:W^opt755_%Y1_%Y2_%Y3^/?y7;[y7 - запас]
!!VRy7|y6=0/y6=2:*2;          [удвоенный запас для дерева/руды]
!!VRy7&y6=6:*1000;            [*1000 для золота]
!!VRy8:S179251 +y6;           [y8 - индекс сообщения с названием шахты]
!!VRz1:Szy8;                  [заголовок сообщения]
!!IF:Q1/y6/y7/4/1;            [вывод сообщения]
!!CM:R0;                      [отключение стандартного действия]

**end
