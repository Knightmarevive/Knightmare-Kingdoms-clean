ZVSE
*  Автор: [igrik]

!?PI;  !!FU87000:P0;
!?GM0; !!FU87000:P1;

!?FU87000;
!!SN:L^EraPlugins\erm_hooker.era^/?y1;   [проверили плагин]
!!FU&y1=0:E;                             [выход, если его НЕТ]
!!SN:Ay1/^SetHook^/?y2;                  [получить адрес функции SetHook]
!!FU87001:Py2;                           [функция установки всех хуков]

!?FU87001; функция хуков (x1 - адрес установки хука)
!!SN:Ex1/1/7710213/87020;                [перед битвой перед триггером BA0]
; ------------------------------------------------------------------------------
** открыть окно встречи героев в городе по ЛКМ + Alt
!?CM1;                              [клик в городе]
!!CM:F?y6 S?y7 I?y5;;                   [получаем парамерты клика]
!!FU|y7<>12/y6<>32:E;                   [нажата ЛКМ + Alt]
!!if|y5=123/y5=125:;                    [место клика]
   !!CA-1:H0/?y1 H1/?y2;                [получить номера героев]
   !!if&y1>-1/y2>-1:;                   [только если есть оба героя]
     !!UN:C6918840/4/?y10;              [получить AdvManager]
     !!SN:E7411341/1/y1;  !!VRy11:Sv1;  [структура первого героя]
     !!SN:E7411341/1/y2;  !!VRy12:Sv1;  [структура второго героя]
     !!SN:E4859312/3/y11/y12;           [грамотность между героями]
     !!SN:E4893280/2/y10/y11/y12;       [открыть окно встречи]
   !!en:;
!!en:;
; ------------------------------------------------------------------------------
** Вкл/откл. быструю битву по клавише "Q" на карте приключений
!?FU77003;                              [нажатие клавиши]
!!FU:E;                                 [disabled by majaczek]
!!SN:X?y1;                              [y1 — код клавиши]
!!FU&y1<>81:E;                          [выйти, если код не 81 (клавиша "Q")]
!!SN:L^Era.dll^/?y2;                    [получили описатель библиотеки Эры в y2]
!!SN:Ay2/^GetGameState^/?y3;            [y3 — адрес функции GetGameState]
!!SN:Ey3/1/?y4;                         [y4 — ID родительского диалога, y5 — текущего]
!!if&y5=4205280:;                       [если это не карта приключений]
  !!UN:C6916044/4/?y1;                  [проверить параметр быстрой битвы 0x6987CC]
  !!VRy1:*-1 +1;                        [вкл/выкл.]
  !!UN:C6916044/4/y1;                   [установить параметр быстрой битвы 0x6987CC]
  !!IF&y1<>0:L^%Z187000^;               [показать описание ВКЛ]
  !!IF&y1=0:L^%Z187001^;                [показать описание ОТКЛ]
!!en:;
!!SN:L^HD_WOG.dll^/?y9;                 [проверить на HD]
!!if&y5=4662240/y9=0:;                  [если это экран битвы]
  !!BA:E?y9;                            [проверить на МП битву]
  !!if&y9=0:;
    !!IF:Q2^%Z187002?^;                 [спросить: закончить битву быстро?]
    !!if&2:;
      !!SN:W^YQBattle^/1;               [запоминалка вернуть ли параметр]
      !!UN:C6916044/4/1;                [установить быструю битву]
    !!en:;
  !!en:;
!!en:;

!?BA53;
!!SN:W^YQBattle^/?y1;                   [прочитать параметр]
!!SN:W^YQBattle^/0;                     [обнулить переменную]
!!UN&y1=1:C6916044/4/0;                 [отключить быструю битву]
; ------------------------------------------------------------------------------
** автосохранение перед битвой
!?FU87020;
!!FU:E;                                  [moved by majaczek]
!!UN:P889/?y1;     !!FU&y1<>1:E;         [проверили опцию ВКЛ/ОТКЛ]
!!UN:P729/?y1;     !!FU&y1<>0:E;         [проверили опцию в ES Rus ВКЛ/ОТКЛ]
!!OW:C?y2 Iy2/?y3; !!FU&y3<>0:E;         [проверили на игрока-человека]
!!UN:C6919480/4/?y1;
!!SN:E4975456/2/y1/^BattleSave!^/1/0/1/0;
; ------------------------------------------------------------------------------
** Оковы Войны (в бою с нейтралами можно убежать с поля боя)
!?BG0; !!BG:A?y1; !!BA:H1/?y2;          [узнать героя защитника]
!!UN&y1=4/y2=-2:C4689325/1/2;           [подменить номер арта]
!?BA52;  !!UN:C4689325/1/125;           [вернуть номер арта]
;-------------------------------------------------------------------------------
** открытие окон героев в битве по ПКМ на дефе героев
!?CM4;
!!CM:F?y1 D?y2;
!!FU&y1<>512:E;
!!FU&y2<252|y3>253:E;
!!BA:H0/?y4 H1/?y5;
!!SN&y2=252:E5118576/3/y4/1/1/1;
!!SN&y2=253/y5>-1:E5118576/3/y5/1/1/1;
!!CM:R0;
;-------------------------------------------------------------------------------
** Быстрая тактическая расстановка
!?CM4;
!!SN:L^HD_WOG.dll^/?y1 L^HD_MCR.dll^/?y2;
!!BU:T?y3;
!!FU|y1<>0/y2<>0/y3<>1:E;
!!CM:F?y2 I?y3 D?y4;
!!FU|y3>10/y2<>0/y4<0/y4>186:E;
!!BU:Ey4/?y5;
!!FU&y5=-1:E;
!!BG:N?y6 Q?y7;
!!BMy5:I?y8 T?y9;
!!FU&y9>144/y9<150:E;
!!FU&y7<>y8|y6=y5:E;
!!VRy5&y7=1:-21;
!!CM:R0;
!!UN:C6919200/4/?y1;
!!SN:E4607760/2/y1/y8/y5;
;-------------------------------------------------------------------------------
; ПКМ описание кнопок Журнал и Рюкзак
!?CM2; !!CM:F?y1 I?y2;
!!CM&y1=512/y2=8000:R0; !!IF&y1=512/y2=8000:M0/4/^%Z187003^;
!!CM&y1=512/y2=128:R0;  !!IF&y1=512/y2=128:M0/4/^%Z187004^;
;-------------------------------------------------------------------------------
!?FU87000;
!!UN:C6052891/1/4; центрируем текст названия города в окне города (id 149)
; Отключение рук командира (скрин в окне героя)
!!UN:P3/?y1;
!!SN&y1=1:L^Era.dll^/?y1 Ay1/^RedirectFile^/?y2 Ey2/1/^heroscr4.pcx^/^heroscr3.pcx^;
; отключение бесплатной дипломатии
!!UN:P891/?y1;
!!UN&y1=0:C4289079/4/2 C4879709/1/127;
!!UN&y1=1:C4289079/4/3 C4879709/1/235;
;-------------------------------------------------------------------------------
; для переигрываемой битвы (функции 870520 и 870530 вызываются из плагина BattleReplay.dll)
; но в них нельзя трогать никакие переменные, иначе будет писец
!?FU870520;  !!FU87052:P;  // начало битвы (только при переигровках)
!?FU870530;  !!FU87053:P;  // конец битвы (всегда после битвы

**                              - end -












;-------------------------------------------------------------------------------
; -----------------------------   Всякое остальное -----------------------------
;-------------------------------------------------------------------------------

; Быстро снять одно Знамя Полоководца со стека и положить в рюкзак по CTRL+ПКМ

!?FU208001;  проверка на клик "me"
!!IF:V115/0;
!!OW:C?y1;
!!OW:Gy1/?y2;
!!IF&y2=1:V115/1;

!?CM2;
!!FU208001:P0;                          // проверка на того кто кликает
!!FU&-115:E;                            // выход
!!CM:A?y1/?y2;                          // координаты клика
!!CM:H?y3/?y4;                          // номер героя
!!CM:I?y5;                              // место клика
!!CM:F?y6 I?y7;                         // тип и подтип клика
!!VRy9:Sy5-68;                          // смещение в слоты
!!FU208003&y5=141/y6=4/y3>-1:Py3;       // настройка знамени
!!FU|y5<68/y5>74:E;                     // выход
!!FU208002&y6=512:Py3/y9;               // ф-ция удаления знамени

!?FU208002;                             // ПКМ в окне героя  (положить знамя в рюкзак)
!!SN:L^user32.dll^/?y1 Ay1/^GetKeyState^/?y2 Ey2/1/17;
!!VRy1:Sv1&65535;                       // проверить на CTRL+ПКМ
!!FU&y1<32768:E;
!!HEx1:C0/x2/?y2/?y3;
!!FU&y2=-1:E;
!!EXx1/x2:R?y4/156/?y5/?y6;
!!if&y4=1:;
  !!CM:R0;
  !!EXx1/x2&y6=0:R0/156/y5/0;
  !!EXx1/x2&y6<>0:R1/156/y5/d-1;
  !!HEx1:A156;
  !!UN:R3/-1;
  !!VRz1:S^Button.wav^;
  !!SN:Pz1;
!!en:;
;-------------------------------------------------------------------------------
* Часы реального времени (автор: feanor)
* Скрипт показывает реальное время и уровень заряда батареи (для ноутбуков)
* по клику правой кнопкой мыши на картинке даты.

!?CM0;
!!CM:I?y1 F?y2;
!!if&y1=1008/y2=512:;
!!CM:R0;
!!VRz1:S^kernel32.dll^;
!!SN:Lz1/?y2;
!!VRz1:S^GetSystemPowerStatus^;
!!SN:Ay2/z1/?y3;
!!SN:Ey3/1/?y4;

!!VRy10:Sy4;
!!VRy10::65536;

!!VRz1:S^GetLocalTime^;
!!SN:Ay2/z1/?y3;
!!SN:Ey3/1/?y4;

!!VRy8:Sy6;
!!VRy9:Sy6;
!!VRy8::65536;
!!VRy9:%65536;

!!IF&y10<>255/y9>9/y8>9:M0/4/^{%Z187005} %Y9:%Y8

{%Z187006} %Y10%%^;
!!IF&y10=255/y9>9/y8>9:M0/4/^{%Z187005} %Y9:%Y8^;

!!IF&y10<>255/y9<10/y8>9:M0/4/^{%Z187005} 0%Y9:%Y8

{%Z187006} %Y10%%^;
!!IF&y10=255/y9<10/y8>9:M0/4/^{%Z187005} 0%Y9:%Y8^;

!!IF&y10<>255/y9<10/y8<10:M0/4/^{%Z187005} 0%Y9:0%Y8

{%Z162023} %Y10%%^;
!!IF&y10=255/y9<10/y8<10:M0/4/^{%Z187005} 0%Y9:0%Y8^;

!!IF&y10<>255/y9>9/y8<10:M0/4/^{%Z187005} %Y9:0%Y8

{%Z162023} %Y10%%^;
!!IF&y10=255/y9>9/y8<10:M0/4/^{%Z187005} %Y9:0%Y8^;
!!en:;

; ------------------------------------------------------------------------------
**  Скрипт убирает все WoG способности, получаемые войсками при накоплении опыта
**  (за исключением основных бонусов: атаки, защиты, урона, скорость, жизни)
!?PI;
!!UN:P890/?y1 P742/?y2;
!!FU&y1<>1|y2=1:E;
!!UN:J11/1;
!!VRy2:S0;
!!UN:C5093986/4/?y3; [y3 кол-во существ в игре]
[:LoopNoWoGAbillity]
!!EAy2:B6/0/////////////;
!!EAy2:B7/0/////////////;
!!EAy2:B8/0/////////////;
!!EAy2:B9/0/////////////;
!!EAy2:B10/0/////////////;
!!EAy2:B11/0/////////////;
!!EAy2:B12/0/////////////;
!!EAy2:B13/0/////////////;
!!EAy2:B14/0/////////////;
!!EAy2:B15/0/////////////;
!!EAy2:B16/0/////////////;
!!EAy2:B17/0/////////////;
!!EAy2:B18/0/////////////;
!!EAy2:B19/0/////////////;
!!VRy2:+1;
!!FU&y2>1000:E;
!!SN&y2<y3:G[LoopNoWoGAbillity];
!!FU:E;
