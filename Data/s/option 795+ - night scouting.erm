ZVSE
ERMS_ScriptDate=13.10(October).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814
** Author orig.  : Algor
** Name          : Night scouting
** Name rus.     : Ночная разведка
** Options       : 795
** Dialogs       : 77
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : FU7916, FU7997-FU7998,FU8001-FU8099
** PO-values     : -

** Перед началом и в процессе игры на некоторых свободных участках карты размещаются случайные события
** найти и активировать которые может любой герой заночевавший на данном или соседнем участке.
** Герои с навыком Разведка и/или артефактами Телескоп и Подзорная труба во время ночлега
** могут искать события на большем количестве соседних участков.
** За одну ночевку активируется не более одного события (даже если в "радиусе поиска" их несколько).
** После активации событие исчезает не зависимо от того, восползовался им герой или нет.
** ИИ использует события, только если они ему выгодны.

** Внимание! Для работы требуется "FUN.erm".

!#TM50:S2/999/1/255;          [каждый день для всех игроков начиная со 2го дня]

!?FU97700;                     [перед началом новой игры или после загрузки старой]
!!UN:P795/?y1;                [проверяем включена ли опция 795 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
** Установка описаний навыка Разведка
!!FU97701:P0/3/1/179331;       [Базовый]
!!FU97701:P0/3/2/179332;       [Продвинутый]
!!FU97701:P0/3/3/179333;       [Эксперт]
** Установка описаний артефактов
!!FU97701:P3/52/1/179334;      [Телескоп]
!!FU97701:P3/53/1/179334;      [Подзорная труба]

!?PI;                         [пост-инструкция: размещение событий]
!!UN:P795/?y1;                [проверяем включена ли опция 795 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!UN:X?y1/?y2;                [y1/y2 - размер/этажность карты]
!!VRy3:Sy2 +1 *y1 *y1 :36;    [y3 - макс. количество генерируемых событий (одно на 36 квадратов карты)]
debug: !_!VRy3:*5;
!!VRy1:-1;                    [y1 - макс. координата]
debug: !_!VRv7:S0;
!!DO97916/1/y3/1:Py1/y2;       [генерация события]
debug: !_!IF:M^Событий размещено: %V7 из %Y3^;

!?TM50;                       [в начале дня начиная со второго]
!!UN:P795/?y1;                [проверяем включена ли опция 795 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
** генерация события (до 1го события за каждого игрока на каждом ходу)
!!UN:X?y1/?y2;                [y1/y2 - размер/этажность карты]
!!VRy1:-1;                    [y1 - макс. координата]
!!FU97916:Py1/y2;              [генерация события]
** обнаружение событий для каждого героя текущего игрока
!!OW:C?y1 Hy1/1/0 Iy1/?y4;    [y1 - текущий игрок, v1 - кол-во героев текущего игрока, y4 - ИИ]
!!VRy2:Sv1;                   [y2 - кол-во героев текущего игрока]
!!UN:X?y3/?i; !!VRy3:-1;      [y3 - макс. координата карты]
!!DO97997/1/y2/1&y2>0:Py1/y4/y3; [поиск событий для каждого героя текущего игрока]

!?FU97997;                     [поиск событий для каждого героя текущего игрока. x1/x2/x3/x16 - игрок/ИИ/макс коорд./номер героя у игрока]
!!OW:Hx1/1/x16;               [v1 - номер героя]
!!VRy7:Sv1;                   [y7 - номер героя]
!!HEy7:P?y1/?y2/?y3 S3/?y4 A2/52/d/?y5 A2/53/d/?y6;  [y1-y3/y4/y5/y6 - коодринаты/уровень Разведки/Телескопы/Подзорные трубы героя]
!!VRy4&y5>0:+1; !!VRy4&y6>0:+1; [y4 - сила разведки (0..5)]
!!VRy4:*4 +5;                 [y4 - количество участков для разведки (5..25)]
!!DO97998/1/y4/1:Px1/x2/x3/y7/y1/y2/y3; [поиск события]

!?FU97998;                     [поиск события]
** x1 - игрок, x2 - ИИ, x3 - макс координата, x4 - герой, x5/x6/x7 - координаты героя, x16 - исследуемый квадрат
!!VRy1:Sx5; !!VRy2:Sx6; !!VRy3:Sx7; [y1/y2/y3 - координаты исследуемого участка]
!!VRy2&x16=2:-1;                    [...]
!!VRy1&x16=3:+1;                    [...]
!!VRy2&x16=4:+1;                    [...]
!!VRy1&x16=5:-1;                    [...]
!!VRy1&x16=6:-1; !!VRy2&x16=6:-1;   [...]
!!VRy1&x16=7:+1; !!VRy2&x16=7:-1;   [...]
!!VRy1&x16=8:+1; !!VRy2&x16=8:+1;   [...]
!!VRy1&x16=9:-1; !!VRy2&x16=9:+1;   [...]
!!VRy2&x16=10:-2;                   [...]
!!VRy1&x16=11:+2;                   [...]
!!VRy2&x16=12:+2;                   [...]
!!VRy1&x16=13:-2;                   [...]
!!VRy1&x16=14:+1; !!VRy2&x16=14:-2; [...]
!!VRy1&x16=15:+2; !!VRy2&x16=15:+1; [...]
!!VRy1&x16=16:-1; !!VRy2&x16=16:+2; [...]
!!VRy1&x16=17:-2; !!VRy2&x16=17:-1; [...]
!!VRy1&x16=18:-1; !!VRy2&x16=18:-2; [...]
!!VRy1&x16=19:+2; !!VRy2&x16=19:-1; [...]
!!VRy1&x16=20:+1; !!VRy2&x16=20:+2; [...]
!!VRy1&x16=21:-2; !!VRy2&x16=21:+1; [...]
!!VRy1&x16=22:-2; !!VRy2&x16=22:-2; [...]
!!VRy1&x16=23:+2; !!VRy2&x16=23:-2; [...]
!!VRy1&x16=24:+2; !!VRy2&x16=24:+2; [...]
!!VRy1&x16=25:-2; !!VRy2&x16=25:+2; [...]
!!FU|y1<0/y2<0/y1>x3/y2>x3:E;       [выход, если координаты за пределами карты]
!!SN:W^o795.%Y1.%Y2.%Y3.T^/?y4;     [y4 - тип события]
!!if&y4>0:;
  !!SN:W^o795.%Y1.%Y2.%Y3.P1^/?y5;  [y5-y9 - параметры события]
  !!SN:W^o795.%Y1.%Y2.%Y3.P2^/?y6;  [...]
  !!SN:W^o795.%Y1.%Y2.%Y3.P3^/?y7;  [...]
  !!SN:W^o795.%Y1.%Y2.%Y3.P4^/?y8;  [...]
  !!SN:W^o795.%Y1.%Y2.%Y3.P5^/?y9;  [...]
  !!VRv1:Sy4 +8000;                 [v1 - номер функции обработчика события]
  !!FUv1:Px1/x4/x2/y1/y2/y3/y5/y6/y7/y8/y9; [вызов функции обработчика]
  !!SN:W^o795.%Y1.%Y2.%Y3.T^/?y4;   [y4 - тип события]
  !!VRx16&y4=0:S100500;             [прерывание поиска событий, если событие активировано]
!!en:;

!?FU97772;                           [триггер: после фазы регенерации; проверки на мораль, страх; и после установки активного стека]
!!SN:W^auto.a^/?y10 W^auto.a^/0 W^auto.b^/?y11 W^auto.c^/?y12 W^auto.s^/?y13; [получение настроек автобоя]
!!UN&y10<>0:Cy10/1/y11 C6916068/1/y12 C6916072/1/y13;                         [восстановление параметров автобоя]
!!SN:W^opt795.10.dt^/?y1 W^opt795.10.dc^/?y2 W^opt795.10.ds^/?y3; [y1/y2/y3 - тип/кол-во/номер отряда защищающихся существ]
!!FU&y2=0:E;                        [выход, если защищающихся существ нет]
!!BG:N?y4;                          [номер активного стека]
!!BMy4:T?y5; !!FU|y4<>y3/y5<>y1:E;  [выход, ходит не защищающийся отряд или если тип отряда не совпадает с типом защищающегося]
!!UN:C6919200/4/?y10;               [y10 - combatmanager]
!!VRy1:Sy10 +78532;                 [y1 - адрес статуса автобоя]
!!UN:Cy1/1/?y11;                    [y11 - статус автобоя]
!!UN:C6916068/1/?y12 C6916068/1/1;  [y12 - статус автобоя - существа, существа ходят автоматически]
!!UN:C6916072/1/?y13 C6916072/1/0;  [y13 - статус автобоя - заклинания, заклинания НЕ кастуются автоматически]
!!SN:W^auto.a^/y1 W^auto.b^/y11 W^auto.c^/y12 W^auto.s^/y13; [сохранение настроек автобоя]
!!SN:E4683792/2/y10;                [включение автобоя]

!?FU98097;                           [вызов битвы]
!!HEx2:Tx4/x5/x6/0/1;               [вызов битвы]

!?FU98098;                           [возврат в v1 номера отряда попадающего под критерии отбора]
** x1-номер героя, x2-x4-критерии отбора
** x2=0/1/2/3/4/5/6 - любой/пеший/стрелок/летун/живой/нежить/элементаль
** x3=0/1/2/3/4 - проверяемый параметр Уровень/FV/Численность/Сила(FV*кол-во)/Опыт
** x4=0/1 - максимум/минимум
!!if&x16=0:;                        [инициализация переменных при первом запуске]
  !!VRv1:S-1;                       [v1 - найденный отряд]
  !!VRv2&x4=0:S-1; !!VRv2&x4=1:S2000000000; [v2 - оцениваемый параметр]
!!en:;
!!HEx1:C0/x16/?y1/?y2/?y3/2;        [y1/y2/y3 - тип/кол-во/опыт существ]
!!FU|y1<0/y2<1:E;                   [выход, если отряда нет]
!!MA:Xy1/?y4;                       [y4 - флаги существа]
!!VRy5:Sy4 &2;                      [y5 - летун]
!!VRy6:Sy4 &16;                     [y6 - живой]
!!VRy7:Sy4 &262144;                 [y7 - нежить]
!!VRy4:&4;                          [y4 - стрелок]
!!FU&x2=1/y4>0:E; !!FU&x2=1/y5>0:E; [выход если тип отряда не совпадает с требуемым]
!!FU&x2=2/y4=0:E; !!FU&x2=3/y5=0:E; [...]
!!FU&x2=4/y6=0:E; !!FU&x2=5/y7=0:E; [...]
!!FU&x2=6/y6>0:E; !!FU&x2=6/y7>0:E; [...]
!!MA&x3=0:Ly1/?y3;                  [y3 - проверяемый параметр]
!!MA|x3=1/x3=3:Fy1/?y3;             [...]
!!VRy3&x3=2:Sy2;                    [...]
!!VRy3&x3=3:*y2;                    [...]
!!VRv1&y3>v2/x4=0:Sx16;             [обновление максимума/минимума/номера отряда]
!!VRv2&y3>v2/x4=0:Sy3;              [...]
!!VRv1&y3<v2/x4=1:Sx16;             [...]
!!VRv2&y3<v2/x4=1:Sy3;              [...]

!?FU98099;                     [формирование и вызов диалога]
** x1-герой, x2/x3-тип/номер НПЦ, x4-кол-во картинок (0..3), x5-переменная для возврата результата выбора (1-да, 2-нет)
** x6/x7-тип/номер левой картинки, x8/x9-тип/номер центральной картинки, x10/x11-тип/номер правой картинки
** Типы: 0-герой, 1-монстр, 2-заклинание, 3-ресурс, 4-перв.параметр, 5-втор.навык/ранг, 6-артефакт
** z6-событие, z7-описание, z8-вопрос, z9-подсказка 1, z10-подсказка 2, z11-подсказка 3
!!VRx5:S0;                    [инициализация]
!!DL77:N^opt795_%X4.txt^;     [подгрузка 77 диалога]
!!if&-1:;                     [если диалог не найден]
  !!IF:M0/4/^ERROR: Dialog not found!^; [выводим сообщение об ошибке]
  !!FU:E;                     [и выходим]
!!en:;
!!DL77:A10/4/x1/1; !!VRv101:Cx1/x2/x3/x4/x5/x6/x7/x8/x9/x10/x11; [установка номера и портрета героя]
!!VRz12:S^%X2-list.def^; !!DL77:A12/9/z12/1 A12/4/x3/1; [установка портрета НПЦ]
!!DL77:A6/3/z6/1; !!DL77:A7/3/z7/1; !!DL77:A8/3/z8/1; [установка текстов]
!!if|x4=2/x4=3:;              [отображение левой картинки (для случая 2 или 3 картинок)]
  !!VRz13:S^%X6-list.def^; !!DL77:A14/9/z13/1 A14/4/x7/1; [установка картинки]
  !!DL77:A15/3/z9/1;          [установка подписи]
!!en:;
!!if|x4=1/x4=3:;              [отображение средней картинки (для случая 1 или 3х картинок)]
  !!VRz14:S^%X8-list.def^; !!DL77:A17/9/z14/1 A17/4/x9/1; [установка картинки]
  !!DL77:A18/3/z10/1;         [установка подписи]
!!en:;
!!if|x4=2/x4=3:;              [отображение левой картинки (для случая 2 или 3 картинок)]
  !!VRz15:S^%X10-list.def^; !!DL77:A20/9/z15/1 A20/4/x11/1; [установка картинки]
  !!DL77:A21/3/z11/1;         [установка подписи]
!!en:;
!!DL77:S?x5;                  [вызов диалога и получение результата выбора (1-да, 2-нет)]

!?DL&v998=77;                 [обработка диалога 77]
!!DL77|v999=1/v999=2:C1;      [кнопки "ОК"/"Отмена"]
!!if&v999=10:;                [портрет героя]
  !!UN:C5119174/1/184 C5119175/4/1;       [патч: деактивация кнопки "Уволить"]
  !!SN:E5118576/1/v101/0;                 [открыть окно героя ЛКМ]
  !!UN:C5119174/1/161 C5119175/4/6916832; [патч: вернуть исходный код кнопки "Уволить"]
!!en:;
!!if&v999=14/v106=6:;         [клик по левой картинке-артефакту]
  !!UN:C6687592/4/?y12;       [y12 - ссылка на таблицу артефактов]
  !!VRy13:Sv107 *32 +y12 +16; [y13 - адрес указателя с описанием артефакта]
  !!UN:Cy13/4/?y14;           [y14 - получить указатель на описание артефакта]
  !!SN:X?y9 Xy14 X?z3 Xy9;    [z3 - хранит описание артефакта]
  !!IF:Q1/8/v107/4^%Z3^;      [вывод окна с картинкой и названием артефакта]
!!en:;

** Генерация событий

!?FU97916;                     [генерация события, x1/x2 - размер/этажность карты]
!!VRy1:S0 Rx1; !!VRy2:S0 Rx1; !!VRy3:S0 Rx2; [y1/y2/y3 - координаты события]
!!OBy1/y2/y3:T?y4;            [y4 - тип объекта в позиции]
!!TRy1/y2/y3:E?y5 P?y6;       [y5/y6 - "желтый"/"красный" квадрат]
!!TRy1/y2/y3:T?y7/d/d/d/d/d/d/d; [y7 - тип почвы]
!!FU|y4>0/y5=0/y6=0/y7>7:E;   [выход, если квадрат не свободен или морской]
debug: !_!UN:Iy1/y2/y3/124/0/0;
debug: !_!VRv7:+1;
** генерация события
!!VRy10:S0; !!VRy11:S0; !!VRy12:S0; !!VRy13:S0; !!VRy14:S0; !!VRy15:S0; [инициализация параметров события]
!!VRy10:S1 R7;                [y10 - тип события (1..6 +10, остальные пока не реализованы)]
!!VRy10&y10>6:S10;            ["конфликт" встречается в 2 раза чаще]
debug: !_!VRy10:S2;
** y11-y15 - параметры события
!!if&y10=1:;                  [Тайник: P1 = тип ресурса, P2 = Кол-во ресурса, P3 = тип охраны, P4 = кол-во охранников]
  !!VRy11:S0 R6;              [тип ресурса]
  !!VRy12:S5 R8;              [кол-во ресурса 5-13 шт]
  !!VRy13:S0 R7 *14 +y12 -5;  [тип охраны]
  !!VRy14:S15 R10;            [кол-во охранников]
  !!VRy12|y11=0/y11=2:*2;     [дерево и руда х2]
  !!VRy12&y11=6:*200;         [золото х200]
!!en:;
!!if&y10=2:;                  [Продавец артефакта: P1 = тип артефакта, P2 = тип ресурса, P3 = Кол-во ресурса]
  !!UN:J6/6/?y11;             [артефакт класса сокровище..ценный]
  !!VRy12:S0 R6;              [тип ресурса]
  !!UN:Ay11/3/?y13;           [класс артефакта (2/4)]
  !!VRy13:*1000;              [стоимость в золоте 2000/4000]
  !!VRy13|y12=0/y12=2::250;   [:250 для стоимости в дереве/руде]
  !!VRy13|y12=1/y12=3/y12=4/y12=5::500; [:500 для стоимости в драг.ресурсах]
!!en:;
!!if&y10=3:;                  [Инструктор: P1-P3 = критерии выбора отряда]
  !!VRy11:S0 R3;              [любой/пеший/стрелок/летун]
  !!VRy12:S0 R4;              [Уровень/FV/Численность/Сила(FV*кол-во)/Опыт]
  !!VRy13:S0 R1;              [максимум/минимум]
!!en:;
!!if&y10=4:;                  [Наставник: P1 = параметр, P2 = увеличение параметра, P3 = тип ресурса, P4 = кол-во ресурса]
  !!VRy11:S0 R3;              [Параметр 0..3]
  !!VRy12:S1 R2;              [увеличение параметра 1..3]
  !!VRy13:S0 R6;              [тип ресурса]
  !!VRy14:Sy12 *5;            [стоимость 5 ресурса за 1 ед параметра]
  !!VRy14&y13=6: *200;        [х200 для Золота]
  !!VRy14|y13=0/y13=2: *2;    [х2 для Дерева/Руды]
!!en:;
!!if&y10=5:;                  [Исчезающий призрак: P1 = требуемое кол-во маны, P2 = тип ресурса, P3 = кол-во ресурса]
  !!VRy11:S1 R4;              [ценность схрона]
!!en:;
!!if&y10=6:;                  [Некромант: P1 = повышение ранга]
  !!VRy11:S0 R3;              [критерий выбора отряда 0..3]
  !!VRy12:S1 R2;              [макс.бонус уровня 1..3]
  !!VRy13:S0 R2;              [грейд 0..2]
!!en:;
!!if&y10=7:;                  [Барахольщик: P1 = тип ресурса]
  !!VRy11:S0 R6;              [тип ресурса]
!!en:;
!!if&y10=8:;                  [Наемники: P1 = тип существ]
  !!VRy11:S0 R7 *14 +4 R9;    [тип существ (ур. 3-7)]
!!en:;
!!if&y10=9:;                  [Вербовщик: P1 = критерий выбора отряда, P2 = множитель цены]
  !!VRy11:S1 R2;              [критерий выбора]
  !!VRy12:S1 R2 *20 +100;     [множитель цены 120/140/160%]
!!en:;
!!if&y10=10:;                 [Конфликт]
  !!VRy16:S0 R8;              [y16 - город защищающихся]
  !!VRy17:Sy16 +3 R3 %9;      [y17 - город атакующих (другое мировоззрение)]
  !!VRy18:S0 R6;              [y18 - уровень защищающихся]
  !!VRy19:S0 R1;              [y19 - грейд защищающихся]
  !!VRy20:Sy18 +1 R2;         [y20 - уровень атакующих (на 1..3 уровня больше, чем защищающихся)]
  !!VRy20&y20>6:S6;           [... максимум 7й уровень]
  !!VRy21:S0 R1;              [y19 - грейд атакующих]
  !!UN:Ty16/y18/y19/?y11;     [y11 - защищающиеся существа]
  !!UN:Ty17/y20/y21/?y12;     [y12 - атакующие существа существа]
  !!VRy13:S4 R8;              [y13 - 3 х коэфф.превосходства (4..12)]
!!en:;
**
!!SN:W^o795.%Y1.%Y2.%Y3.T^/y10;  [установка параметров события]
!!SN:W^o795.%Y1.%Y2.%Y3.P1^/y11; [...]
!!SN:W^o795.%Y1.%Y2.%Y3.P2^/y12; [...]
!!SN:W^o795.%Y1.%Y2.%Y3.P3^/y13; [...]
!!SN:W^o795.%Y1.%Y2.%Y3.P4^/y14; [...]
!!SN:W^o795.%Y1.%Y2.%Y3.P5^/y15; [...]

** Обработчики событий - FU80##
** x1 - игрок, x2 - герой, x3 - 0/1=Человек/ИИ, x4-x6 - координаты события, x7-x11 - параметры события

!?FU98001;                     [Тайник]
** Обнаружение тайника с ресурсами
** P1 = тип ресурса, P2 = Кол-во ресурса, P3 = тип охраны, P4 = кол-во охранников
** проверка условия срабатывания
!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]
!!if&x3=1:;                   [обработчик игрока ИИ]
  !!OW:Rx1/x7/dx8;            [ИИ получает ресурсы без боя]
!!el:;                        [обработчик игрока-человека]
  !!VRy7:S0;
** z6-событие, z7-описание, z8-вопрос, z9-подсказка 1, z10-подсказка 2, z11-подсказка 3
  !!VRz6:Sz179306; !!VRz7:Sz179307; !!VRz8:Sz179308; [получение описаний события]
  !!VRy1:Sx8 -3 R7; !!VRy1&x7=6:Sx8 -600 R1200 :100 *100; !!VRy2:Sx10 -5 R11; [примерные количества ресурса и охраны]
  !!VRz9:S^~ %Y1^; !!VRz11:S^~ %Y2^; [формирование подсказок]
  !!FU98099:Px2/1/x9/2/?y7/3/x7/0/0/1/x9; [вызов диалога и получение ответа в y7 (1-да,2-нет)]
  !!FU&y7<>1:E;               [выход, если человек отказался от активации события]
  !!HEx2:Tx4/x5/x6/x9/x10;    [вызов битвы]
  !!HEx2:O?y1;                [y1 - владелец героя]
  !!if&y1<>-1:;               [если герой жив]
    !!OW:Rx1/x7/dx8; !!UN:R2; [игрок получает ресурсы]
    !!VRz1:Sz179309;          [z1 - текст сообщения]
    !!IF:Q1/x7/x8/1/1;        [вывод сообщения]
  !!en:;
!!en:;

!?FU98002;                     [Продавец артефакта]
** Продавец артефакта за ресурсы
** P1 = тип артефакта, P2 = тип ресурса, P3 = Кол-во ресурса
** проверка условия срабатывания
!!OW:Rx1/x8/?y5;              [y5 - кол-во требуемого ресурса у игрока]
!!FU&y5<x9:E;                 [событие не случается, если ресурса недостаточно]
!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]
!!if&x3=1:;                   [обработчик игрока ИИ]
  !!HEx2:A2/x7/?y1/?y2;       [y1/y2 - кол-во артефактов предлагаемого типа у героя]
  !!UN:Ax7/2/?y3;             [y3 - позиция предлагаемого артефакта]
  !!HEx2:A1/?y10/0 A1/?y11/1 A1/?y12/2 A1/?y13/3 A1/?y14/4 A1/?y15/5 A1/?y16/6 A1/?y17/7 A1/?y18/8; [Артефакты на герое]
  !!HEx2:A1/?y19/9 A1/?y20/10 A1/?y21/11 A1/?y22/12 A1/?y23/18;                                     [...]
  !!VRy1&y3=1/y10>=0:S1;      [проверка занятости необходимого слота]
  !!VRy1&y3=2/y11>=0:S1;      [...]
  !!VRy1&y3=3/y12>=0:S1;      [...]
  !!VRy1&y3=4/y13>=0:S1;      [...]
  !!VRy1&y3=5/y14>=0:S1;      [...]
  !!VRy1&y3=6/y15>=0:S1;      [...]
  !!VRy1&y3=7/y16>=0/y17>=0:S1; [...]
  !!VRy1&y3=8/y18>=0:S1;      [...]
  !!VRy1&y3=9/y19>=0/y20>=0/y21>=0/y22>=0/y23>=0:S1; [...]
  !!FU|y1>0/y2>0:E;           [выход, если артефакт уже есть у героя или все доступные для него слоты заняты]
  !!VRy5:-x9;                 [вычитание стоимости артефакта]
  !!OW:Rx1/x8/y5;             [обновление ресурсов игрока]
  !!HEx2:A4/x7;               [передача артефакта герою]
!!el:;                        [обработчик игрока-человека]
  !!VRy1:Sx8 +179311;         [y1 - индекс переменной с описанием события]
  !!VRz6:Sz179310; !!VRz7:Szy1; !!VRz8:Sz179318; [получение описаний события]
  !!VRy1&x8=0:S16;            [тип НПЦ в зависимости от ресурса]
  !!VRy1&x8=1:S32;            [...]
  !!VRy1&x8=2:S144;           [...]
  !!VRy1&x8=3:S50;            [...]
  !!VRy1&x8=4:S64;            [...]
  !!VRy1&x8=5:S169;           [...]
  !!VRy1&x8=6:S143;           [...]
  !!VRz9:S^+^; !!VRz11:S^- %X9^; [формирование подсказок]
  !!FU98099:Px2/1/y1/2/?y7/6/x7/0/0/3/x8; [вызов диалога и получение ответа в y7 (1-да,2-нет)]
  !!FU&y7<>1:E;               [выход, если человек отказался от активации события]
  !!VRy5:-x9;                 [вычитание стоимости артефакта]
  !!OW:Rx1/x8/y5; !!UN:R2;    [обновление ресурсов игрока]
  !!HEx2:A4/x7;               [передача артефакта герою]
!!en:;

!?FU98003;                     [Инструктор]
** Максимизация опыта отряда за деньги и время
** P1-P3 = критерии выбора отряда
!!UN:P900/?y1; !!FU&y1=0:E;   [выход, если опыт отрядов не включен]
!!DO98098/0/6/1:Px2/x7/x8/x9;  [возврат в v1 номера отряда попадающего под критерии отбора]
!!VRy1:Sv1; !!FU&y1<0:E;      [y1 - найденный отряд. Выход, если подходящего отряда нет]
!!HEx2:C0/y1/?y2/?y3/?y4/2;   [y2/y3/y4 - тип/кол-во/опыт отряда]
!!EAy2:L?y5;                  [y5 - опыт 10го ранга]
!!FU&y4>=y5:E;                [выход, если отряд уже имеет 10 ранг опыта]
!!VRy6:Sy5 -y4;               [y6 - необходимое количество опыта]
!!VRy7:Sy4 *100 :y5 -100 *-1; [y7 - % опыта, который надо получить отряду до 10 ранга]
!!MA:Fy2/?y8;                 [y8 - FV существ в отряде]
!!VRy8:*y3;                   [y8 - FV отряда]
!!VRy8:*y7 :500;              [y8 - стоимость тренировки FV/500 за 1% опыта]
!!VRy8&y8<100:S100;           [не менее 100 золотых]
!!OW:Rx1/6/?y9;               [y9 - золото текущего игрока]
!!FU&y9<y8:E;                 [выход, если средств не достаточно]
!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]
!!if&x3=1:;                   [обработчик игрока ИИ]
  !!VRy10:Sy9 :2;             [y10 - половина золотого запаса ИИ]
  !!FU&y8>y10:E;              [выход, если цена превышает половину имеющегося золота]
  !!MA:Ly2/?y10;              [y10 - уровень существ в отряде (0..6)]
  !!VRy10:*-1 +7 *y10 *4;     [y10 - мин. кол-во сущест для тренировки (98/72/50/32/18/8/2 существ соотв. 1..7 уровня]
  !!HEx2:C0/y1/d/?y11;        [y11 - кол-во существ в тренируемом отряде]
  !!FU&y11<y10:E;             [выход, если существ недостаточно для тренировки]
  !!VRy8:*-1;                 [y8 - золото для списания]
  !!OW:Rx1/6/dy8;             [списание средств]
  !!HEx2:C0/y1/d/d/y5/2 W0/1; [максимизация опыта отряда и трата ОД]
!!el:;                        [обработчик игрока-человека]
  !!VRy10:Sx7 +179320;        [y10 - индекс переменной с описанием события]
  !!VRz6:Sz179319; !!VRz7:Szy10; !!VRz8:Sz179324; [получение описаний события]
  !!VRz9:S^%Y3^; !!VRz10:S^+ %Y6^; !!VRz11:S^- %Y8^; [подписи к картинкам]
  !!VRy11&x7=0:S169;          [y11 - Существо-инструктор, Фанатик]
  !!VRy11&x7=1:S91;           [Огр-шаман]
  !!VRy11&x7=2:S137;          [Снайпер]
  !!VRy11&x7=3:S109;          [Виверн]
  !!FU98099:Px2/1/y11/3/?y12/1/y2/4/4/3/6; [вызов диалога и получение ответа в y12 (1-да,2-нет)]
  !!FU&y12<>1:E;              [выход, если человек отказался от активации события]
  !!VRy8:*-1;                 [y8 - золото для списания]
  !!OW:Rx1/6/dy8;  !!UN:R2;   [списание средств]
  !!HEx2:C0/y1/d/d/y5/2 W0/1; [максимизация опыта отряда и трата ОД]
!!en:;

!?FU98004;                     [Наставник]
** Тренировка первичного параметра героя за ресурсы
** P1 = параметр, P2 = увеличение параметра, P3 = тип ресурса, P4 = кол-во ресурса
!!OW:Rx1/x9/?y1;              [y1 - кол-во необходимого ресурса у игрока]
!!FU&x10>y1:E;                [выход, если ресурса недостаточно]
!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]
!!if&x3=1:;                   [обработчик игрока ИИ]
  !!VRy2:Sy1 :2;              [y2 - половина запаса ресурса у ИИ]
  !!FU&x10>y2:E;              [выход, если цена превышает половину имеющегося ресурса]
  !!VRy8:Sx10 *-1;            [y8 - средства к списанию]
  !!OW:Rx1/x9/dy8;            [списание средств]
  !!HEx2&x7=0:Fdx8/d/d/d;     [повышение первичного параметра героя]
  !!HEx2&x7=1:Fd/dx8/d/d;     [...]
  !!HEx2&x7=2:Fd/d/dx8/d;     [...]
  !!HEx2&x7=3:Fd/d/d/dx8;     [...]
  !!VRy2:Sx8 *-300;           [y2 - стоимость ОД (300 за 1 одно параметра)]
  !!HEx2:Wdy2/1;              [...]
!!el:;                        [обработчик игрока-человека]
  !!VRy10:Sx7 +179326;        [y10 - индекс переменной с описанием события]
  !!VRz6:Sz179325; !!VRz7:Szy10; !!VRz8:Sz179330; [получение описаний события]
  !!VRz9:S^+ %X8^; !!VRz10:S^^; !!VRz11:S^- %X10^; [подписи к картинкам]
  !!VRy11&x7=0:S7;            [y11 - Существо-Тренер, Крестоносец]
  !!VRy11&x7=1:S17;           [Боевой гном]
  !!VRy11&x7=2:S136;          [Чародей]
  !!VRy11&x7=3:S8;            [Монах]
  !!FU98099:Px2/1/y11/2/?y12/4/x7/0/0/3/x9; [вызов диалога и получение ответа в y12 (1-да,2-нет)]
  !!FU&y12<>1:E;              [выход, если человек отказался от активации события]
  !!VRy8:Sx10 *-1;            [y8 - средства к списанию]
  !!OW:Rx1/x9/dy8; !!UN:R2;   [списание средств]
  !!HEx2&x7=0:Fdx8/d/d/d;     [повышение первичного параметра героя]
  !!HEx2&x7=1:Fd/dx8/d/d;     [...]
  !!HEx2&x7=2:Fd/d/dx8/d;     [...]
  !!HEx2&x7=3:Fd/d/d/dx8;     [...]
  !!VRy2:Sx8 *-300;           [y2 - стоимость ОД (300 за 1 одно параметра)]
  !!HEx2:Wdy2/1;              [...]
!!en:;

!?FU98005;                     [Исчезающий призрак]
** Покупатель маны/пп героя за ресурсы
** P1 = ценность схрона
!!HEx2:I?y1/1;                [y1 - кол-во маны у героя]
!!VRy2:Sx7 *20;               [y2 - требуемое кол-во маны 20*ценность схрона]
!!FU&y2>y1:E;                 [выход, если маны недостаточно]
!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]
!!VRy3:Sy2 *-1;               [y3 - отрицательное значение маны]
!!VRy10:S1 Rx7;               [установка случайного кол-ва ресурсов]
!!VRy11:S1 Rx7;               []
!!VRy12:S1 Rx7;               []
!!VRy13:S1 Rx7;               []
!!VRy14:S1 Rx7;               []
!!VRy15:S1 Rx7;               []
!!VRy16:S1 Rx7 *100;          []
!!UN:P36/?y17;                [y17 - статус опции "Мифрил"]
!!VRy17&y17>0:S1 Rx7;         [кол-во мифрила, если включен]
!!if&x3=1:;                   [обработчик игрока ИИ]
** ИИ жертвует ману, только если это не больше половины от имеющейся у него в даный момент
  !!VRy3:Sy1 :2;              [y3 - половина маны героя]
  !!FU&y2>y1:E;               [выход, если требуется более половины имеющейся маны]
  !!HEx2:Idy3/1;              [уменьшение маны героя]
  !!OW:Rx1/0/dy10 Rx1/1/dy11 Rx1/2/dy12 Rx1/3/dy13 Rx1/4/dy14 Rx1/5/dy15 Rx1/6/dy16 Rx1/7/dy17; [добавление ресурсов]
!!el:;                        [обработчик игрока-человека]
  !!VRz6:Sz179337; !!VRz7:Sz179338; !!VRz8:Sz179339; [получение описаний события]
  !!VRz9:S^- %Y2^; !!VRz10:S^^; !!VRz11:S^???^; [подписи к картинкам]
  !!FU98099:Px2/1/61/2/?y12/4/5/0/0/3/8; [вызов диалога и получение ответа в y12 (1-да,2-нет)]
  !!FU&y12<>1:E;              [выход, если человек отказался от активации события]
  !!HEx2:Idy3/1;              [уменьшение маны героя]
  !!OW:Rx1/0/dy10 Rx1/1/dy11 Rx1/2/dy12 Rx1/3/dy13 Rx1/4/dy14 Rx1/5/dy15 Rx1/6/dy16 Rx1/7/dy17; [добавление ресурсов]
  !!UN:R2;                    [обновление строки ресурсов]
  !!VRz1:Sz179340;            [z1 - текст посещения схрона]
  !!IF&y17>0:N1/y11/3/y13/4/y14/5/y15/0/y10/2/y12/6/y16/7/y17; [схрон с мифрилом]
  !!IF&y17=0:N1/y11/3/y13/4/y14/5/y15/0/y10/6/y16/2/y12;       [схрон без мифрила]
  !!IF:N1/1;                  [показ диалога получения ресурсов]
!!en:;

!?FU98006;                     [Некромант]
** Преобразование самого многочисленного живого отряда в нежить
** P1 = критерий выбора отряда, P2 = макс.бонус уровня, P3 = грейд
!!DO98098/0/6/1&x7=0:Px2/4/0/1;  [возврат в v1 номера самого низкоуровневого живого отряда]
!!DO98098/0/6/1&x7=1:Px2/4/2/0;  [возврат в v1 номера самого многочисленного живого отряда]
!!DO98098/0/6/1&x7=2:Px2/4/3/1;  [возврат в v1 номера самого слабого живого отряда]
!!DO98098/0/6/1&x7=3:Px2/4/4/1;  [возврат в v1 номера самого неопытного живого отряда]
!!VRy1:Sv1; !!FU&y1<0:E;        [выход, если подходящего отряда не найдено]
!!HEx2:C0/y1/?y2/?y3;           [y1/y2/y3 - отряд/тип/кол-во требуемых существ]
!!MA:Ly2/?y4;                   [y4 - уровень требуемых существ.]
!!VRy4:+x8; !!VRy4&y4>7:S7;     [y4 - уровень преобразованных существ (не более 7. 7 - Драколичи)]
** y5 - вид новых существ (уровень/грейд)
!!VRy5&y4=1/x9=0:S58; !!VRy5&y4=1/x9=1:S59; !!VRy5&y4=1/x9=2:S141; [2й уровень: Мертвецы/Зомби/Мумии]
!!VRy5&y4=2/x9=0:S60; !!VRy5&y4=2/x9=1:S61; !!VRy5&y4=2/x9=2:S159; [3й уровень: Стражи/Призраки/Привидения]
!!VRy5&y4=3/x9=0:S62; !!VRy5&y4=3/x9=1:S63; !!VRy5&y4=3/x9=2:S63;  [4й уровень: Вампиры/Л.Вампиры/Л.Вампиры]
!!VRy5&y4=4/x9=0:S64; !!VRy5&y4=4/x9=1:S65; !!VRy5&y4=4/x9=2:S65;  [5й уровень: Личи/М.Личи/М.Личи]
!!VRy5&y4=5/x9=0:S66; !!VRy5&y4=5/x9=1:S67; !!VRy5&y4=5/x9=2:S172; [6й уровень: Черн.рыцарь/Р.Смерти/Кошмары]
!!VRy5&y4=6/x9=0:S68; !!VRy5&y4=6/x9=1:S69; !!VRy5&y4=6/x9=2:S154; [7й уровень: Кост.драконы/Призр.драконы/Кров.Драконы]
!!VRy5&y4=7:S196;                                                  [8й уровень: Драколичи]
!!MA:Fy2/?y6 Fy5/?y7;         [y6/y7 - FV старого/нового существа]
!!VRy8:Sy3 *y6 :y7 +1; !!VRy8&y8>y3:Sy3; [y8 - кол-во новых существ (не менее 1го и не более старого кол-ва)]
!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]
!!if&x3=1:;                   [обработчик игрока ИИ]
  !!FU&y4<3:E;                [ИИ не соглашается на преобразование ниже Вампиров]
  !!HEx2:C0/y1/y5/y8;         [трансформация существ]
!!el:;                        [обработчик игрока-человека]
  !!VRz6:Sz179341; !!VRz7:Sz179342; !!VRz8:Sz179343; [получение описаний события]
  !!VRz9:S^- %Y3^; !!VRz10:S^^; !!VRz11:S^+ %Y8^; [подписи к картинкам]
  !!FU98099:Px2/0/150/2/?y12/1/y2/0/0/1/y5; [вызов диалога и получение ответа в y12 (1-да,2-нет)]
  !!FU&y12<>1:E;              [выход, если человек отказался от активации события]
  !!HEx2:C0/y1/y5/y8;         [трансформация существ]
  !!UN:R1;                    [обновление экрана]
  !!VRz1:Sz179344;            [z1 - текст сообщения]
  !!IF:M1/1;                  [вывод сообщения]
!!en:;

!?FU98007;                     [Барахольщик]
** Покупка самого дешевого артефакта из рюкзака героя за ресурсы
** P1 = тип ресурса
debug: !!IF&x3=0:M^Событие не реализовано: Барахольщик^;
** проверка условия срабатывания
!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]
!!if&x3=1:;                   [обработчик игрока ИИ]
!!el:;                        [обработчик игрока-человека]
!!en:;

!?FU98008;                     [Наемники]
** Увеличивающийся со временем отряд странствующих наемников присоединяется за плату
** P1 = тип существ
debug: !!IF&x3=0:M^Событие не реализовано: Наемники^;
** проверка условия срабатывания
!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]
!!if&x3=1:;                   [обработчик игрока ИИ]
!!el:;                        [обработчик игрока-человека]
!!en:;

!?FU98009;                     [Вербовщик]
** Вербовщик выкупает у героя самый слабый отряд существ
** P1 = критерий выбора отряда, P2 = множитель цены
debug: !!IF&x3=0:M^Событие не реализовано: Наемники^;
** проверка условия срабатывания
!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]
!!if&x3=1:;                   [обработчик игрока ИИ]
!!el:;                        [обработчик игрока-человека]
!!en:;

!?FU98010;                     [Конфликт]
** Схватка 2х неравных по силе сторон с возможностью помочь слабой стороне за вознаграждение
** P1 = Защищающиеся Существа, P2 = Атакующие существа, P3 = утроенный коэффициент превосходства атакующих (4..12),
!!MA:Lx7/?y1 Fx7/?y2;         [y1/y2 - уровень/FV защищающихся]
!!VRy4:Sy1 +1 *y4;            [y4 - делитель = уровень^2]
!!VRy3:Sc *3 :y4 +1;          [y3 - кол-во защищающихся = (день*3)/(уровень^2)+1]
!!VRy4:Sy3 *y2 *x9 :3;        [y4 - общее FV атакующих]
!!MA:Fx8/?y5;                 [y5 - FV атакующего существа]
!!VRy4::y5 +1;                [y4 - кол-во атакующих]
!!VRy5:*y4;                   [y5 - общее FV атакующих]
!!MA:Cx7/6/?y6; !!VRy6::2;    [y6 - вознаграждение за каждого спасенного: 1/2 золотой части стоимости]
!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]
!!if&x3=1:;                   [обработчик игрока ИИ]
  !!HEx2:C0/0/?y11/?y21 C0/1/?y12/?y22 C0/2/?y13/?y23 C0/3/?y14/?y24 C0/4/?y15/?y25 C0/5/?y16/?y26 C0/6/?y17/?y27; [Армия ИИ]
  !!MA&y11>=0:Fy11/?y31; !!VRy31:*y21;   [ценности слотов армии ИИ]
  !!MA&y12>=0:Fy12/?y32; !!VRy32:*y22;   [...]
  !!MA&y13>=0:Fy13/?y33; !!VRy33:*y23;   [...]
  !!MA&y14>=0:Fy14/?y34; !!VRy34:*y24;   [...]
  !!MA&y15>=0:Fy15/?y35; !!VRy35:*y25;   [...]
  !!MA&y16>=0:Fy16/?y36; !!VRy36:*y26;   [...]
  !!MA&y17>=0:Fy17/?y37; !!VRy37:*y27;   [...]
  !!VRy31:+y32 +y33 +y34 +y35 +y36 +y37; [y31 - FV армии ИИ]
  !!VRy32:Sy31 :2;            [y32 - 1/2 силы армии ИИ]
  !!VRy33:Sy31 :5;            [y33 - 1/5 силы армии ИИ]
  !!FU&y32<y5:E;              [выход, если армия ИИ не более чем в 2 раза сильнее атакующих]
  !!if&y33<y5:;               [если армия ИИ сильнее атакующих от 2 до 5 раз, ИИ получает ресурсы за половину "спасенных"]
    !!VRy34:Sy3 :2 *y6;       [y34 - вознаграждение за половину "спасенных"]
    !!OW:Rx1/6/dy34;          [вознаграждение ИИ]
  !!el:;                      [если армия ИИ сильнее атакующих более чем в 5 раз, ИИ получает ресурсы/наемников за 100% "спасенных"]
    !!VRy34:S-1;
    !!HEx2&y34<0/y11<0:C0/0/x7/y3; !!FU&y34<0/y11<0:E; [добавление "спасенных" в первый сводобный слот армии ИИ]
    !!HEx2&y34<0/y12<0:C0/1/x7/y3; !!FU&y34<0/y12<0:E; [...]
    !!HEx2&y34<0/y13<0:C0/2/x7/y3; !!FU&y34<0/y13<0:E; [...]
    !!HEx2&y34<0/y14<0:C0/3/x7/y3; !!FU&y34<0/y14<0:E; [...]
    !!HEx2&y34<0/y15<0:C0/4/x7/y3; !!FU&y34<0/y15<0:E; [...]
    !!HEx2&y34<0/y16<0:C0/5/x7/y3; !!FU&y34<0/y16<0:E; [...]
    !!HEx2&y34<0/y17<0:C0/6/x7/y3; !!FU&y34<0/y17<0:E; [...]
    !!DO98098/0/6/1:Px2/0/3/1; [возврат в v1 номера самого слабого отряда ИИ]
    !!VRy37:Sv1;              [y37 - номер самого слабого отряда ИИ]
    !!VRy34:Sv1 +11; !!VRy35:Sy34 +10; [y34/y35 - индексы y-переменных с типом/кол-вом существ в отряде]
    !!MA:Fyy34/?y36;          [y36 - FV существа в отряде]
    !!VRy36:*yy35 *3;         [y36 - утроенное FV самого слабого отряда]
    !!VRy38:Sy3 *y2;          [y38 - общее FV "спасенных"]
    !!HEx2&y36<y38:C0/y37/x7/y3; [если FV спасенных минимум втрое превышает FV самого слабого отряда, ИИ заменяет ими самый слабый отряд]
    !!FU&y36<y38:E;           [выход, если существа присоединены]
    !!VRy34:Sy3 *y6;          [y34 - вознаграждение за 100% "спасенных"]
    !!OW:Rx1/6/dy34;          [вознаграждение ИИ]
  !!en:;
!!el:;                        [обработчик игрока-человека]
  !!UN:N3/1/x7/1; !!UN:N3/2/x8/1; [z1/z2 - наименования существ (мн.ч.)]
  !!VRz6:Sz179345; !!VRz7:Sz179346; !!VRz8:Sz179347; [получение описаний события]
  !!VRy31:Sy3 -1 R2; !!VRy31&y31<1:S1; [y31 - примерное число защищающихся (не менее 2)]
  !!VRy32:Sy4 -1 R2; !!VRy32&y32<1:S1; [y32 - примерное число атакующих (не менее 2)]
  !!VRz9:S^~ %Y31^; !!VRz10:S^^; !!VRz11:S^~ %Y32^; [подписи к картинкам]
  !!FU98099:Px2/4/0/3/?y12/1/x7/4/0/1/x8; [вызов диалога и получение ответа в y12 (1-да,2-нет)]
  !!FU&y12<>1:E;              [выход, если человек отказался от активации события]
  !!SN:W^opt795.10.dt^/x7;    [установка параместров конфликта]
  !!SN:W^opt795.10.dc^/y3;    [...]
  !!SN:W^opt795.10.at^/x8;    [...]
  !!SN:W^opt795.10.ac^/y4;    [...]
  !!HEx2:R4/?y35 R4/0;        [y35 - статус Тактики героя, запрет тактической расстановки]
  !!FU98097:Px1/x2/x3/x4/x5/x6;[вызов боя с сохранением значений переменных]
  !!HEx2:R4/y35;              [восстановление статуса Тактики героя]
  !!SN:W^opt795.10.dc^/?y31 W^opt795.10.dc^/0;     [y31 - кол-во защищающихся существ / сброс кол-ва]
  !_!IF:M^debug: Результаты боя: %Y31 / %Y3^;
  !!HEx2:O?y30;               [y30 - принадлежность героя (-1 - проиграл бой)]
  !!FU|y30<0/y31<1:E;         [выход, если герой не победил в стражении или никого не удалось спасти]
  !!VRy32:Sy31 *y6;           [y32 - вознаграждение]
  !!UN:N3/1/x7/0;             [z1 - название существа (ед.ч.)]
  !!if&y31<>y3:;              [если удалось спасти не всех]
    !!VRz2:Sz179348;          [z2 - текст сообщения]
    !!IF:Q1/6/y32/1/2;        [показ сообщения]
    !!OW:Rx1/6/dy6;           [добавление золота]
    !!UN:R2;                  [обновление строки ресурсов]
  !!el:;                      [если удалось спасти всех]
    !!VRz2:Sz179349;          [z2 - текст сообщения]
    !!VRy33:Sy3 *65536 +x7;   [y33 - кол-во существ в подписи]
    !!IF:Q1/21/y33/6/y32/7/2; [показ сообщения]
    !!if&1:;                  [если выбраны существа]
      !!HEx2:C2/x7/y3/1;      [добавление существ герою]
      !!UN:R1;                [обновление экрана]
    !!el:;                    [если выбрано золото]
      !!OW:Rx1/6/dy6;         [добавление золота]
      !!UN:R2;                [обновление строки ресурсов]
    !!en:;
  !!en:;
!!en:;

!?BA52;
!!SN:W^opt795.10.dc^/?y2;     [y2 - кол-во защищающихся существ в конфликте]
!!FU&y2=0:E;                  [выход, если не конфликт]
!!BAQ0;                       [отключениe быстрой битвы]
!!SN:W^opt795.10.at^/?y4 W^opt795.10.ac^/?y5; [y4/y5 - тип/кол-во атакующих]
!!VRy12:Sy5 :2 *2; !!VRy13:Sy5 :3 *3; [y9 - количество атакующих отрядов (1..3)]
!!VRy9:S1; !!VRy9&y12=y5:S2; !!VRy9&y13=y5:S3; [...]
!!VRy10:Sy5 :y9;              [y10 - кол-во существ в атакующих отрядах]
!!BA:M1/0/y4/y10;             [установка атакующих существ]
!!BA&y9>1:M1/1/y4/y10;        [...]
!!BA&y9>2:M1/2/y4/y10;        [...]

!?BF;
!!SN:W^opt795.10.dt^/?y1 W^opt795.10.dc^/?y2; [y1/y2 - тип/кол-во атакующих]
!!FU&y2=0:E;                  [выход, если не конфликт]
!!BF:C;                       [удаление преград]
!!BU:Sy1/y2/94/0/-1/1 E94/?y3;[призыв защищающихся существ y3 - номер отряда]
!!SN:W^opt795.10.ds^/y3;      [сохранение номера защищающегося отряда]

!?BG1;
!!SN:W^opt795.10.dt^/?y1 W^opt795.10.dc^/?y2 W^opt795.10.ds^/?y3;
!!FU&y2=0:E;                  [выход, если не конфликт]
!!BU:C?y6;                    [y6=1, если бой завершен]
!!FU&y6=0:E;                  [выход, если бой не завершен]
!!BMy3:N?y4 T?y5 G-81/?y6/d;  [y4/y5/y6 - кол-во/тип/безвозвратные потери]
!!VRy4:-y6; !!VRy4&y4<0:S0;   [y4 - реальное кол-во защищавшихся]
!!VRy4&y5<>y1:S0;             [обнуление кол-ва защищавшихся, если отряд тругого типа]
!!SN:W^opt795.10.dc^/y4;      [обновление кол-ва защищавшихся]
!_!IF:M^debug: Выжило %Y4 из %Y2^;

!?FU977006;
!!SN:W^opt795.10.dt^/?y1 W^opt795.10.dc^/?y2 W^opt795.10.ds^/?y3;
!!FU&y2=0:E;                  [выход, если не конфликт]
!!SN:X?y4/?y5; !!VRy4:*21 +y5;[y4 - номер отряда]
!!BMy4:T?y5;                  [y5 - тип отряда]
!!FU|y5<>y1/y4<>y3:E;         [выход, если отряд не "защищающийся"]
** включение автобоя?

** end
